%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                              %
%   HIGZ  User Guide -- LaTeX Source                                           %
%                                                                              %
%   Chapter: The input routines                                                %
%                                                                              %
%   Editor: Olivier Couet / CN-AS                                              %
%   Last Mod.:  9 July oc                                                      %
%                                                                              %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Filename{H1Theinputroutines}
\chapter{The input routines}
\index{input routines}
 
\Filename{H2Cursorinput}
\section{Cursor input}
\index{cursor input}
 
\subsection{The Generic Routine}
\Shubr[GKS]{IRQLC}{(KWKID,LCDNR,ISTAT*,NT*,PX*,PY*)}
\Action
This routine returns the \Lit{(x,y)} position of the cursor in \WC, and the
index the \NT. Its calling sequence is compatible with the equivalent \GKS{}
routine.
\Pdesc
\begin{DLtt}{1234567}
\item[KWKID]Workstation identifier.
\item[LCDNR]Locator device.
\begin{DLtt}{123}
\item[1] Keyboard.
\item[2] Graphic tablet.
\end{DLtt}
With the \X11{} driver \Lit{LCDNR} can have the following values:
\begin{DLtt}{123}
\item[10] tracking cross
\item[20] cross-hair
\item[30] rubber circle
\item[40] rubber band
\item[50] rubber rectangle
\item[99] the screen coordinates are taken in \Lit{XLOC} and \Lit{YLOC}.
\item[>0] request mode
\item[<0] sample mode
\end{DLtt}
\item[ISTAT] Return status.
\begin{DLtt}{123}
\item[0] Graphic input has been canceled.
\item[1] A point was located and its coordinates are recorded in
\Lit{PX} and \Lit{PY}.
\end{DLtt}
\item[NT] Index of the \NT.
\item[PX] X coordinate of position of locator
\item[PY] Y coordinate of position of locator
\end{DLtt}
 
\subsection{The Two Points Routine}
\Shubr{IGLOC2}{(KWKID,*NT*,X1*,Y1*,X2*,Y2*,ISTAT*,CHOPT)}
\Action
This routine returns the graphic cursor position in \WC{} space of two points
and the corresponding \NT{} number. Rubberbanding is used to visualize the area
(box) delimited by the two points.
\index{Graphics Input and transformations}
\Pdesc
\begin{DLtt}{1234567}
\item[KWKID] Workstation identifier
\item[NT] Index of the \NT see{}(\Lit{CHOPT}).
\item[X1] X coordinate of the cursor position in \WC~space of the first point.
\item[Y1] Y coordinate of the cursor position in \WC~space of the first point.
\item[X2] X coordinate of the cursor position in \WC~space of the second point.
\item[Y2] Y coordinate of the cursor position in \WC~space of the second point.
\item[ISTAT] Return status:
\begin{DLtt}{123}
\item[0] Graphic input has been canceled.
\item[1] Two points were located and their coordinates are recorded in
         \Lit{X1, Y1, X2, Y2}.
\end{DLtt}
\item[CHOPT] \Lit{CHARACTER} variable specifying the option desired:
\begin{DLtt}{12345}
\item[' '] \Lit{NT} is an output parameter.
\item['P'] \Lit{NT} is an input and output parameter. In this case,
           \Lit{NT} contains on input the \NT~index with the highest priority.
\end{DLtt}
\end{DLtt}
 
\subsection{How to get the position both in \NDC~and \WC~space}
\Shubr{IGLOC}{(ICURS,NT*,IBN*,XNDC*,YNDC*,XWC*,YWC*)}
\Action
It is sometimes useful to get a point position both in \NDC~and \WC~space
at the same time. This routine allows to do this for the workstation 1.
\begin{DLtt}{1234567}
\item[ICURS] Cursor type.
\item[NT] \NT~number.
\item[IBN] Button number:
\begin{DLtt}{123}
\item[0] Right button of the mouse.
\item[1] Left button of the mouse.
\item[3] Middle button of the mouse only for the X11 interface.
\end{DLtt}
\item[XNDC] X coordinate of the cursor position in \NDC{} space.
\item[YNDC] Y coordinate of the cursor position in \NDC{} space.
\item[XWC] X coordinate of the cursor position in \WC{} space.
\item[YWC] Y coordinate of the cursor position in \WC{} space.
\end{DLtt}
 
 
\Filename{H2Keyboardinput}
\section{Keyboard input}
\index{keyboard input}
\Shubr[GKS]{IRQST}{(KWKID,ISTDNR,ISTAT*,L*,STR*)}
\Action
This routine returns a character string typed on the keyboard.
\Pdesc
\begin{DLtt}{1234567}
\item[KWKID]  Workstation identifier. If \Lit{KWKID} is negative, the 
              parameters \Lit{RQUEST(81)}, \Lit{RQUEST(82)}, \Lit{RQUEST(91)},
              and \Lit{RQUEST(92)} given via the \Lit{QUEST COMMON} specify
              a box in \NDC{} in which the request string will be done. 
              If \HIGZ{} is installed with \GKS{} an ``initialise string''
              is performed.
\item[ISTDNR] Device number
\item[ISTAT]  Return status. \Lit{0}: Break and \Lit{1}: OK
\item[L]      Number of characters returned
\item[STR]    Character string returned
\end{DLtt}
\par
Note that in the routines \Rind{IRQLC} and \Rind{IRQST} the parameter 
\Lit{ISTAT} may be used to identify the button number of the mouse.

\newpage
\Filename{H2MenusInput}
\section{Menus Input}
\index{Menus Input}
\Shubr{IGMENU}{(MN,CHTIT,*X1*,*X2*,*Y1*,*Y2*,NBU,CHUSER\-,N,\-CHITEM,\-
             \-CHDEF,\-CHVAL*,ICHOIC*,CHOPT)}
\Action
This routine displays a menu and returns the user's choice in the variable
\Lit{ICHOIC} according to the option chosen. This routine works only on one
menu: the menu management must be performed by the application program but this
routine provides some facilities to manage several menus simultaneously.
\Pdesc
\begin{DLtt}{1234567}
\item[MN] Menu number. To use segment capabilities of the workstation.
If \Lit{MN=0} the segments are not used.
\item[CHTIT] Menu title.
\item[X1] X coordinate of lower left hand corner of menu box
\item[Y1] Y coordinate of lower left hand corner of menu box
\item[X2] X coordinate of upper right hand corner of menu box
\item[Y2] Y coordinate of upper right hand corner of menu box
\item[NBU] Number of User squares.
\item[CHUSER] \Lit{CHARACTER} array of length \Lit{NBU}
containing the text in the users' squares.
The last line of the menu is split into \Lit{NBU} boxes.
\item[N] Number of items.
\item[CHITEM] \Lit{CHARACTER} array of length \Lit{N}
containing the text for the items.
\item[CHDEF] \Lit{CHARACTER} array of length \Lit{N}
containing the text for the parameters.
If \Lit{CHOPT='P'} the menu is split into two columns.
The left column contains the items and
the right column the default value of the corresponding
item.
\Lit{CHDEF(I) (1<I\(\leq\)N)} is a character string which contains
the possible values of the item number \Lit{I}:
\Lit{CHDEF(I)='value1, value2, value3,..., valueN'}.
If \Lit{CHDEF(I)=' '} there are no default values.
\item[CHVAL*] \Lit{CHARACTER} array of length \Lit{N}
into which parameter values are written.
If \Lit{CHOPT='P'} then \Lit{CHVAL(I)} contains the
parameter value for item \Lit{I}.
\item[ICHOIC] Choice number. The description of the possible values returned
in \Lit{ICHOIC} is given in the following table:
\begin{center}
\begin{tabular}{||c|l||}
\hline
0       & Outside of the menu \\
\hline
-100    & Title bar           \\
\hline
-1,NBU  & User keys           \\
\hline
-1000   & Right button of the mouse clicked \\
\hline
 $> 0$  & Item number         \\
\hline
\end{tabular}
\end{center}

\item[CHOPT] \Lit{CHARACTER} variable specifying the option(s) selected.
\end{DLtt}
The square at the left of the title bar moves and resizes the menu.
The square at the right of the title bar moves the menu.

\newpage

\begin{Tabhere}
\begin{center}
\begin{tabular}{||c|p{12cm}||}
\hline
'H'& The picked item is highlighted. The last choice number must be given
     in ICHOIC.\\
\hline
'D'& Display the menu.\\
\hline
'C'& Permit a choice in the displayed menu.\\
\hline
'E'& Erase the menu.\\
\hline
'P'& The menu is a menu with parameters.\\
\hline
'R'& Return the current position of the menu in \Lit{X1,X2,Y1,Y2}.\\
\hline
'S'& Software characters are used to draw the text in the menu.\\
\hline
'U'& Update the user text in the user squares with the value in \Lit{CHUSER}.
     The user square number is given in ICHOIC. The options 'U'
     and 'H' are incompatible because they used both
     ICHOIC as input parameter.\\
\hline
'M'& Menu drawn on a Metafile.\\
\hline
'Z'& Menu stored in the ZEBRA picture.\\
\hline
'N'& The last input position is used to find the menu item.
     With this option choices can be made in several menus
     at the same time using a \Lit{DO} loop as shown below.
     \Lit{NBMENU} is the number of menus on the screen.\\
\hline
'B'& A rubberbanding box is used for the locator.\\
\hline
'T'& The title bar is not drawn, then the menu can not be moved interactively.\\
\hline
'W'& The menu is drawn with Width. \\
\hline
'A'& The menu is drawn with shAdow. \\
\hline
'V'& Draw only the vertical part of width or shadow.\\
\hline
'O'& Like option 'V' but the width or shadow is aligned on the menu frame.\\
\hline
'I'& Input menu. A parameter menu is displayed and \Rind{IGMENU} is
     entered directly in request string. This is useful to
     perform a request string without a very complicated
     initialization part.\\
\hline
'K'& Key menu. The user keys are drawn as key.\\
\hline
\end{tabular}
\end{center}
\caption{Options for \protect\Rind{IGMENU}}
\label{tab-IGMENU}
\end{Tabhere}

\subsection{Example}

This example program shows how \Rind{IGMENU} can manage
several menus at the same time.

\begin{XMPt}{How to manage several menus}
      PROGRAM MENU
*
      COMMON /PAWC/H(50000)
      PARAMETER (NBMENU=3)
      CHARACTER*10 CHU, CHI, CHD, CHV, CHTIT, CHOPT
      CHARACTER*80 TEXT
      CHARACTER*16 CHLOC(3)
      DIMENSION CHU(3),NBU(NBMENU),NBI(NBMENU)
      DIMENSION CHI(3),CHD(3),CHV(3),CHTIT(NBMENU)
      DIMENSION X1(NBMENU),X2(NBMENU),Y1(NBMENU),Y2(NBMENU)
*     Last choice in the menu NB i (useful for HIghligth)
      DIMENSION ICCH(NBMENU)
      DATA CHU /'Quit','Exit','GED'/
      DATA CHI /'Choice 1', '|Choice 2', 'Choice 3'/
*.______________________________________
*
*
*       Initialize \HIGZ
*
      CALL MZEBRA(-3)
      CALL MZPAW(50000,' ')
      CALL IGINIT(0)
      CALL IGWKTY(KWKTYP)
      CALL IGSSE(6,KWKTYP)
      CALL ISELNT(0)
      CALL MESSAGE('Example of the IGMENU usage in multiple input')
*
*       Initialize and display menu number 1
*
  1   ICCH(1)=0
      X1(1)=0.14
      X2(1)=0.35
      Y1(1)=0.1
      Y2(1)=0.25
      NBU(1)=2
      NBI(1)=3
      CHTIT(1)='MENU 1'
      CALL IGMENU (0,CHTIT(1),X1(1),X2(1),Y1(1),Y2(1),NBU(1),CHU,
     +             NBI(1),CHI,CHD,CHV,ICH,'S   D')
*
*       Initialize and display menu number 2
*
      ICCH(2)=0
      X1(2)=0.3
      X2(2)=0.56
      Y1(2)=0.3
      Y2(2)=0.45
      NBU(2)=2
      NBI(2)=3
      CHTIT(2)='MENU 2'
      CALL IGMENU (0,CHTIT(2),X1(2),X2(2),Y1(2),Y2(2),NBU(2),CHU,
     +             NBI(2),CHI,CHD,CHV,ICH,'S   D')
*
*       Initialize and display menu number 3
*
      ICCH(3)=0
      X1(3)=0.05
      X2(3)=0.95
      NBU(3)=3
      NBI(3)=0
      CHTIT(3)='MENU 3'
      Y1(3)=0.9
      Y2(3)=0.935
      CALL IGMENU (0,CHTIT(1),X1(3),X2(3),Y1(3),Y2(3),NBU(3),CHU,
     +             NBI(3),CHI,CHD,CHV,ICH,'ST  D')
*
*       Initialize the current menu number
*
      IMENU=3
*
*       Request in the current menu
*
   10 CONTINUE
      IF(IMENU.LT.3)THEN
         CHOPT='S   CR'
      ELSE
         CHOPT='ST  C'
      ENDIF
      ICH=ICCH(IMENU)
      CALL IGMENU (0,CHTIT(IMENU),X1(IMENU),X2(IMENU),
     +             Y1(IMENU),Y2(IMENU),NBU(IMENU),CHU,
     +             NBI(IMENU),CHI,CHD,CHV,ICH,CHOPT)
*
*       If the choice is outside the menu (ICH=0), we search here
*       if the input is in an other menu (CHOPT='N')
*
      IF(ICH.EQ.0)THEN
         DO 20  I=1,NBMENU
            IF(I.LT.3)THEN
               CHOPT='S CRN'
            ELSE
               CHOPT='SCTNKU'
            ENDIF
            ICH=ICCH(I)
            CALL IGMENU (0,CHTIT(I),X1(I),X2(I),Y1(I),Y2(I),
     +                   NBU(I),CHU,
     +                   NBI(I),CHI,CHD,CHV,ICH,CHOPT)
            IF(ICH.NE.0)THEN
               IMENU=I
               GOTO 30
            ENDIF
   20    CONTINUE
*
*       After the DO loop the input is outside all menus
*
         CALL MESSAGE('Outside the menus')
         GOTO 10
      ENDIF
      ICCH(IMENU)=ICH
*
*       Analyses the result
*
   30 CONTINUE
      IF(ICH.GT.0)THEN
         WRITE(TEXT,'(''Menu : '',I1,'', choice : '',I1)')IMENU,ICH
         CALL MESSAGE(TEXT)
         GOTO 10
      ENDIF
      IF(ICH.EQ.-100)THEN
         WRITE(TEXT,'(''Menu : '',I1,'', title bar'')')IMENU
         CALL MESSAGE(TEXT)
         GOTO 10
      ENDIF
      IF(ICH.EQ.-1000)THEN
         CALL MESSAGE('Right button of the mouse')
         GOTO 10
      ENDIF
      IF(ICH.EQ.-1)THEN
         WRITE(TEXT,'(''QUIT from menu : '',I1)')IMENU
         CALL MESSAGE(TEXT)
         CALL IGEND
         GOTO 999
      ENDIF
      IF(ICH.EQ.-2)THEN
         WRITE(TEXT,'(''EXIT from menu : '',I1)')IMENU
         CALL MESSAGE(TEXT)
         CALL IGEND
         GOTO 999
      ENDIF
      IF(ICH.EQ.-3)THEN
         CALL MESSAGE('Invoke the Graphics Editor')
         CALL IZPICT('*','S')
         CALL IZPICT('P1','M')
         CALL IGRNG(20.,20.)
         CALL IZGED('P1','S')
         GOTO 1
      ENDIF
      IF(ICH.LT.0)THEN
        WRITE(TEXT,'(''Menu : '',I1,'', choice : '',I2)')IMENU,ICH
         CALL MESSAGE(TEXT)
         GOTO 10
      ENDIF
*
  999 END
 
      SUBROUTINE MESSAGE(TEXT)
      CHARACTER*(*) TEXT
      CALL IGZSET('G')
      CALL ISELNT(0)
      CALL IGSET('FACI',0.)
      CALL IGSET('FAIS',1.)
      CALL IGSET('BORD',1.)
      CALL IGBOX(0.,1.,0.,0.04)
      CALL IGSET('TXAL',23.)
      CALL IGSET('CHHE',0.02)
      CALL IGSET('TXFP',-100.)
      CALL ITX(0.5,0.02,TEXT)
      call iuwk(0,0)
      END
\end{XMPt}
