<?xml version='1.0'?>
<!-- makehtml.xsl --> 
<!DOCTYPE xsl:stylesheet [
<!ENTITY lt     "<"> 
<!ENTITY gt     ">"> 
<!ENTITY br     "\n">
<!ENTITY nbsp   "&#160;">
]>
<!-- XSL Style sheet, DTD omitted -->

<xsl:stylesheet
	xmlns:xsl="http://www.w3.org/XSL/Transform/1.0"
	xmlns:nt="http://www.jclark.com/xt"
        extension-element-prefixes="xt">

<xsl:output method="html"/>
<xsl:preserve-space elements="*"/>

<xsl:template name="makeheading">
  <xsl:choose>
    <xsl:when test="ancestor::appendix">
      <xsl:number count="chapter" format="A."/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:variable name="n"> <!-- one plus the chapter number -->
        <xsl:number level="any" count="chapter"/>
      </xsl:variable>
      <xsl:number expr="$n - 1" format="1."/>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:choose>
    <xsl:when test="ancestor::section">
      <xsl:number level="multiple" 
                  count="section|subsection"
                  format="1.1. "/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:text> </xsl:text>
    </xsl:otherwise>
  </xsl:choose>
  <xsl:apply-templates/>
</xsl:template>

<xsl:template name="sectionhead">
  <xsl:param name="level" select="1"/>
  <xsl:element name="H{$level}">
  <xsl:choose>    
    <xsl:when test="../@id">
      <A NAME="{../@id}">
      <xsl:call-template name="makeheading"/>
      </A>
    </xsl:when>
    <xsl:otherwise>
      <xsl:call-template name="makeheading"/>
    </xsl:otherwise>
  </xsl:choose>
  </xsl:element>
</xsl:template>

<xsl:template match="/">
<xsl:apply-templates/>
</xsl:template>

<!--   ================ Frontmatter element ======================= -->
<xsl:template match="frontmatter">
</xsl:template>

<!-- ===================== Bodymatter element ================= -->
<xsl:template match="bodymatter|part|chapter|section|subsection|
                     subsubsection|paragraph|subparagraph">
  <xsl:apply-templates/>
</xsl:template>
<xsl:template match="appendix">
<H2>Appendices</H2>
  <xsl:apply-templates/>
</xsl:template>

<!-- ========= Section headings ============ -->
<xsl:template match="part/stitle">
<H2>!PART! <xsl:number count="part" format="I. "/>
  <xsl:apply-templates/></H2>
</xsl:template>
<xsl:template match="chapter/stitle">
<xsl:text>
</xsl:text>
<xsl:comment>filename=<xsl:value-of select="../@id"/>
</xsl:comment>
<xsl:text>
</xsl:text>
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="1"/>
  </xsl:call-template>
</xsl:template>
<xsl:template match="section/stitle">
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="2"/>
  </xsl:call-template>
</xsl:template>
<xsl:template match="subsection/stitle">
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="3"/>
  </xsl:call-template>
</xsl:template>
<xsl:template match="subsubsection/stitle">
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="4"/>
  </xsl:call-template>
</xsl:template>
<xsl:template match="paragraph/stitle">
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="5"/>
  </xsl:call-template>
</xsl:template>
<xsl:template match="subparagraph/stitle">
  <xsl:call-template name="sectionhead">
    <xsl:with-param name="level" select="6"/>
  </xsl:call-template>
</xsl:template>

<!-- ========= Emphasis ============ -->

<xsl:template match="emph">
<EM><xsl:apply-templates/></EM>
</xsl:template>
<xsl:template match="ucom">
<B><xsl:apply-templates/></B>
</xsl:template>
<xsl:template match="textbf">
<B><xsl:apply-templates/></B>
</xsl:template>
<xsl:template match="textsc">
  <xsl:apply-templates/>
</xsl:template>
<xsl:template match="textsf">
  <xsl:apply-templates/>
</xsl:template>
<xsl:template match="textsl">
<I><xsl:apply-templates/></I>
</xsl:template>
<xsl:template match="texttt|code">
<TT><xsl:apply-templates/></TT>
</xsl:template>
<!-- ======== Cross-references ======== -->
<xsl:template match="cite|pageref|ref">
<A HREF="#{@refid}"><xsl:value-of select="@refid"/></A>
</xsl:template>
<!-- ====== quotes, footnotes, verbatim ====== -->
<xsl:template match="footnote">
<xsl:text>(</xsl:text><xsl:apply-templates/><xsl:text>)</xsl:text>
</xsl:template>
<xsl:template match="footnote|quote|quotation">
<BLOCKQUOTE>
<xsl:apply-templates/>
</BLOCKQUOTE>
</xsl:template>
<xsl:template match="sq">
<xsl:text>"</xsl:text>
<xsl:apply-templates/>
<xsl:text>"</xsl:text>
</xsl:template>
<xsl:template match="verbatim|alltt">
<TABLE><TR><TD CLASS="PRE"><PRE><xsl:apply-templates/></PRE></TD></TR></TABLE>
</xsl:template>
<xsl:template match="listing">
<TABLE><TR><TD CLASS="LISTING"><PRE><xsl:apply-templates/></PRE></TD></TR></TABLE>
</xsl:template>
<xsl:template match="lprout">
<TABLE><TR><TD CLASS="LPROUT"><PRE><xsl:apply-templates/></PRE></TD></TR></TABLE>
</xsl:template>
<xsl:template match="verb">
<TT><xsl:apply-templates/></TT>
</xsl:template>
<!-- ============ Lists ============== -->
<xsl:template match="description|descriptiona">
<DL>
<xsl:apply-templates/>
</DL>
</xsl:template>
<xsl:template match="description/term | descriptiona/term">
<DT><xsl:apply-templates/></DT>
</xsl:template>
<xsl:template match="description/item | descriptiona/item">
<DD><xsl:apply-templates/></DD>
</xsl:template>
<xsl:template match="enumerate|bibliography">
<OL>
<xsl:apply-templates/>
</OL>
</xsl:template>
<xsl:template match="itemize">
<UL>
<xsl:apply-templates/>
</UL>
</xsl:template>
<xsl:template match="enumerate/item|itemize/item|bibliography/bibitem">
<LI><xsl:apply-templates/></LI>
</xsl:template>
<!-- ======== inline and display math =========== -->
<xsl:template match="inlinemath">
<!--Leave as LaTeX text in tt -->
<xsl:apply-templates/>
</xsl:template>
<xsl:template match="displaymath|equation|eqnarray|array">
<PRE>
<!--Leave as LaTeX text in pre -->
<xsl:apply-templates/>
</PRE>
</xsl:template>
<!-- ===== everything that can go into a paragraph ===== -->
<xsl:template match="par">
<P>
<xsl:apply-templates/>
</P>
</xsl:template>
<!-- ===== tabular ===== -->
<xsl:template match="tabular">
<TABLE>
<xsl:apply-templates/>
</TABLE>
</xsl:template>
<xsl:template match="tabular/row">  
<TR>
<xsl:apply-templates/>
</TR>
</xsl:template>
<xsl:template match="tabular/row/cell">  
<TD>
<xsl:apply-templates/>
</TD>
</xsl:template>
<!-- ======= "floats" and their contents ========= -->
<xsl:template match="figure|table">
<xsl:apply-templates/>
</xsl:template>
<xsl:template match="figure/caption | table/caption">
  <P><B><xsl:apply-templates/></B></P>
</xsl:template>
<xsl:template match="includegraphics">
<!-- <P><OBJECT ID="{ancestor(figure)/@id}" DATA="{@file}.gif"
   TYPE="image/gif" /></P> -->
<P CLASS="CENTERP"><A NAME="{ancestor::figure/@id}"><IMG SRC="{@file}.gif"/></A></P>
</xsl:template>
<!-- url -->
<xsl:template match="url">
<A HREF="{@name}">here</A>
</xsl:template>
<!-- Index commands (LaTeX-only) -->
<xsl:template match="index">
</xsl:template>
<!-- small, texonly -->
<xsl:template match="small">
</xsl:template>
<xsl:template match="texonly">
</xsl:template>
</xsl:stylesheet>

