MACRO A
next
OPT NBOX
OPT ZFL1
* Draw a general ZEBRA structure
size 20. 20.
igset bord 1.                         | Draw border of boxes
igset LWID 4.

SELNT 1

*  Define aliases to act as global variables

xbox1  = 1.5                          | length of box
xbox2  = 0.75                         | half of length
xstep  = 2.0                          | distance between boxes in x
ybox1  = 1.0                          | height of box
ybox2  = 0.5                          | half of height

asize  = 0.15                         | width of arrow

txtit  = 0.23                         | height title text
fttit  = -20.                         | font title (Times bold)

igset TXAL 10                         | Left adjust text
igset TXFP [fttit]                    | font definition
igset CHHE [txtit]                    | Define the character height
igset TXAL 20                         | Centre text in x only
igset TXAL 23                         | Centre text in x and y

y1  = 0.0                             | box bottom
y2  = [y1] + [ybox1]                  | box top
yc  = [y1] + [ybox2]                  | box centre in y
yh  = [y2] - $SIGMA([ybox2]*0.5)
yl  = [y1] + $SIGMA([ybox2]*0.5)
yd  = [y2]                            | top of DOWN row
                                        
******  Bottom row of boxes (DOWN) *************

*   BOX 1

x1  = 4.5                             | box left
x2  = [x1] + [xbox1]                  | box right
xa1 = [x2]                            | arrow beginning
xc  = [x1] + [xbox2]                  | box centre in x
xd  = [x1]                            | beginning of DOWN row
xc1 = [xc]                            | centre of bank 1

box [x1] [x2] [y1] [y2]
itx [xc] [yc] 'DOWN'                  | centre label
itx $SIGMA([x2]-0.2) $SIGMA([y1]+0.2) '4'

*   BOX 2

x1  = [x2] + [xstep]                  | box left
x2  = [x1] + [xbox1]                  | box right
arrow [xa1] [x1] [yh] [yh] [asize]    | arrow next
itx $SIGMA(([xa1]+[x1])*0.5) $SIGMA([yh]+0.2) 'n'
arrow [x1] [xa1] [yl] [yl] [asize]    | arrow origin
itx $SIGMA(([xa1]+[x1])*0.5) $SIGMA([yl]-0.2) 'o'
xa1 = [x2]                            | arrow beginning
xc  = [x1] + [xbox2]                  | box centre in x
xc2 = [xc]                            | centre of bank 2

box [x1] [x2] [y1] [y2]
itx [xc] [yc] 'DOWN'                  | centre label
itx $SIGMA([x2]-0.2) $SIGMA([y1]+0.2) '3'

*   BOX 3

x1  = [x2] + [xstep]                  | box left
x2  = [x1] + [xbox1]                  | box right
arrow [xa1] [x1] [yh] [yh] [asize]    | arrow next
itx $SIGMA(([xa1]+[x1])*0.5) $SIGMA([yh]+0.2) 'n'
arrow [x1] [xa1] [yl] [yl] [asize]    | arrow origin
itx $SIGMA(([xa1]+[x1])*0.5) $SIGMA([yl]-0.2) 'o'
xa1 = [x2]                            | arrow beginning
xc  = [x1] + [xbox2]                  | box centre in x
xc3 = [xc]                            | centre of bank 3

box [x1] [x2] [y1] [y2]
itx [xc] [yc] 'DOWN'                  | centre label
itx $SIGMA([x2]-0.2) $SIGMA([y1]+0.2) '4'

x1  = [x1] + [xstep]                  | box left
arrow [xa1] [x1] [yc] [yc] [asize]    | arrow next
itx $SIGMA(([xa1]+[x1])*0.5) $SIGMA([yc]+0.2) 'n'
fttit  = -130.                        | font title (Times Roman)
igset TXAL 13                         | Left adjust text
igset TXFP [fttit]                    | font definition
itx $SIGMA([x1]+0.2) [yc] 'zero'
itx $SIGMA([xa1]+0.2) [y1] 'new bank'

******  Central row of boxes (VX)  ***************

fttit  = -20.                         | font title (Times bold)

igset TXFP [fttit]                    | font definition
igset TXAL 23                         | Centre text in x and y

y1  = 2.0                             | box bottom
y2  = [y1] + [ybox1]                  | box top
yc  = [y1] + [ybox2]                  | box centre in y

*   BOX 1

x1  = 2.                              | box left
x2  = [x1] + [xbox1]                  | box right
xc  = [x1] + [xbox2]                  | box centre in x
xl  = $SIGMA([x1]+[xbox2]*0.5)
xr  = $SIGMA([x2]-[xbox2]*0.5)

box [x1] [x2] [y1] [y2]
itx [xc] [yc] 'MAST'                  | centre label
itx $SIGMA([x2]-0.2) $SIGMA([y1]+0.2) '1'

* connect down and origin from MAST to DOWN

LINE  [xd] [yh] [xr] [yh]
ARROW [xr] [xr] [yh] [y1] [asize]
itx $SIGMA([xr]+0.2) $SIGMA([yh]+0.5) 'o'
LINE  [xl] [y1] [xl] [yl]
ARROW [xl] [xd] [yl] [yl] [asize]
itx $SIGMA([xl]+0.2) $SIGMA([yh]+0.5) 'd'
fttit  = -130.                        | font title (Times Roman)
igset TXFP [fttit]                    | font definition
igset TXAL 33                         | Right adjust x and Centre y 
itx $SIGMA([xl]-0.2) $SIGMA([yh]+0.5) 'link 3'


* connect all DOWN banks up to MAST

igset TXAL 10                         | left adjust x and bottom y
fttit  = -20.                         | font title (Times bold)
igset TXFP [fttit]                    | font definition


ARROW [xc3] [x2] [yc] [yc] [asize]
LINE  [xc1] [yd] [xc1] [yc]
itx $SIGMA([xc1]+0.2) [y1] 'up'
LINE  [xc2] [yd] [xc2] [yc]
itx $SIGMA([xc2]+0.2) [y1] 'up'
LINE  [xc3] [yd] [xc3] [yc]
itx $SIGMA([xc3]+0.2) [y1] 'up'

* connect link area LMAST down to MAST bank

xm  = 1.
ym  = 4.

LINE  [xm] [ym] [xm] [yc]
ARROW [xm] [x1] [yc] [yc] [asize]

igset TXAL 23                         | centre x and y
ym  = 4.3
itx [xm] [ym] 'LMAST'
fttit  = -130.                        | font title (Times Roman)
igset TXFP [fttit]                    | font definition
ym  = 4.8
itx [xm] [ym] 'link area'

PI/LI


