<HEAD>
<TITLE> Example of using ZEBRA and the debug routines</TITLE>
</HEAD>
<BODY><P>
 <HR> <A NAME=tex2html15 HREF=tex2htmlindex2_2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/next_motif.gif"></A> <A NAME=tex2html13 HREF=ZEBDZ2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/up_motif.gif"></A> <A NAME=tex2html7 HREF=ZEBDZ2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/previous_motif.gif"></A>  <A NAME=tex2html17 HREF=tex2htmlindex2_2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/index_motif.gif"></A><BR>
<B> Next:</B> <A NAME=tex2html16 HREF=tex2htmlindex2_2.html>Index</A>
<B>Up:</B> <A NAME=tex2html14 HREF=ZEBDZ2.html>No Title</A>
<B> Previous:</B> <A NAME=tex2html8 HREF=ZEBDZ2.html>No Title</A>
<HR> <P>
<H1><A NAME=SECTION0010000000000000000> Example of using ZEBRA and the debug routines</A></H1>
<P>
<A NAME=sech1dzexamples><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/invis_anchor.xbm"></A>
<P>
In the example below, after initializing ZEBRA with a call to <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZEBRA>MZEBRA</A>,
a store is declared with <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZSTOR>MZSTOR</A> and a link area with <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZLINK>MZLINK</A>.
<P>
Then the data structure shown in figure <A HREF=#GENSTRC><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/cross_ref_motif.gif"></A> is built.
To simplify matters only default
settings for the ZEBRA routine parameters are used. 
Since the store
is the first one declared its store index is 0 and its default
divisions will have indices 1 and 2. 
Not specifying the division index
to <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZLIFT>MZLIFT</A> will create all banks in the present example in division 2.
<P>
After creation of the ``mother'' bank at <tt>LEV</tt>, 
a double DO loop
creates first 3 <tt>VX</tt> (vertex) banks
as down banks, and then attaches respectively 3, 2 and 2 <tt>TK</tt>
(track) banks to the <tt>VX</tt> banks as downs. 
All <tt>VX</tt> banks and the <tt>TK</tt> banks
connected to a given vertex
are grouped together in a linear structure.
The data part of each bank is filled with information of a type specified
in the calls to <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZFORM>MZFORM</A>.
<P>
At the end of creating the data structure 
the complete tree of the <tt>EV</tt> data structure is printed,
followed by a map of division 2 and a detailed verification step 
of the same division.
<P>
Then a <tt>VX</tt> branch of the data structure id dropped with <A HREF=../../zebra/ZEBMZ1/chapter2_1.html#MZDROP>MZDROP</A>
and the droppped banks can now be clearly seen from the map. Then we change
a data word in the top bank, which can be detected by calls to <A HREF=../../zebra/ZEBDZ1/chapter2_1.html#DZCHST>DZCHST</A>.
<P>
Finally we ``overwrite'' a link in the first <tt>VX</tt> bank, and since
the <tt>V</tt> option is set with <A HREF=../../zebra/ZEBDZ1/chapter2_1.html#DZVERI>DZVERI</A>, we get a fatal error and exit
to <A HREF=../../zebra/ZEBMZ3/chapter2_1.html#ZFATAL>ZFATAL</A>, with its associated traceback (obtained on the Apollo in this case)
and a dump of the relevant memory areas.
<P>
<b>Example of building a data structure</b><PRE><TT>PROGRAM ZEXAM

      COMMON//IFENCE(2),LEV,BLVECT(10000)
      COMMON/MYLINK/LLVX(10),LLTK(10,10)
      COMMON/\QUEST/\IQUEST(100)

      DIMENSION LQ(999),IQ(999)
      DIMENSION  Q(999)
      EQUIVALENCE (IQ(1),Q(1),LQ(9)),(LQ(1),LEV)
 
      DIMENSION MMEV(5),MMTK(5),MMVX(5)
      DIMENSION NTK(3)
      DIMENSION ISUM(6)
*       Bank lift parameters for three kind of banks 
      DATA MMEV/4HEV  ,7,7,10,0/
      DATA MMTK/4HTK  ,0,0,15,3/
      DATA MMVX/4HVX  ,1,1,12,0/
*       Number of VX and EV banks to be created 
      DATA NTK/3,2,2/ , NVX/3/
 
C--     Initialize ZEBRA store
 
      CALL MZEBRA(0)
 
C--     Initialize store in blank common //
 
      CALL MZSTOR(IXBLST,'//',' ',IFENCE,LEV,BLVECT(1),BLVECT(1),
     X            BLVECT(2000),BLVECT(10000)                )
 
C--     Initialize link area with reference pointers to all banks
 
      CALL MZLINK(0,'/MYLINK/',LLVX(1),LLVX(1),LLTK(10,10))

C****** Create tree structure in default division (2) *********
 
C--       Bank format descriptions for EV and VX banks
 
      CALL MZFORM('EV  ','3H 3I -F',MMEV(5))
      CALL MZFORM('VX  ','3I -F',MMVX(5))
 
C--       Lift top event bank (EV) of structure and fill with data
 
      CALL MZLIFT(0,LEV,LEV,1,MMEV,0)
 
      IQ(LEV+1) = MMEV(1)
      IQ(LEV+2) = MMTK(1)
      IQ(LEV+3) = MMVX(1)
      DO 1 I=4,6
    1 IQ(LEV+I) = I
      DO 2 I=7,MMEV(4)
    2 Q(LEV+I) = FLOAT(I)
 
C--       Create linear chain of vertex (VX) banks hanging from EV
 
      DO 20 IVX=1,NVX
          CALL MZLIFT(0,LVX,LEV,-1,MMVX,0)
          LLVX(IVX) = LVX
          DO 7 I=1,3
    7     IQ(LVX+I) = 10*IVX+I
          DO 8 I=4,MMVX(4)
    8     Q(LVX+I) = FLOAT(10*IVX+I)
 
C--       Create linear chain of track (TK) banks hanging from
C--              each VX bank
 
          DO 10 ITK=1,NTK(IVX)
              CALL MZLIFT(0,LTK,LVX,-1,MMTK,0)
              LLTK(IVX,ITK) = LTK
              DO 9 I=1,MMTK(4)
    9         Q(LTK+I) = FLOAT(100*ITK+10*IVX+I)
   10     CONTINUE
   20 CONTINUE
 
C--     Print the complete structure and the store, then verify complete
 
      CALL DZSHOW('Dump EV structure',0,LEV,'BDLV',0,0,0,0)
      CALL DZSHOW('Dump EV structure',0,LEV,'BSLV',0,0,0,0)
      CALL DZSTOR('Dump of store //',0)
      CALL DZSURV('Survey of the EV data structure',0,LEV)
      CALL DZSNAP('Snap of //',2,'M')
      CALL DZVERI('Verify default division in //',2,'CFLSU')

C--     Drop the second VX bank and its descendants

      CALL MZDROP(0,LLVX(2),'V')

      CALL DZSURV('Survey of the EV data structure after drop',0,LEV)
      CALL DZSNAP('Snap of // after drop',2,'M')

C--     Check the contents of the data structure

      CALL DZCHST('Check before',0,LEV,'L',ISUM)

*         Change the data part and check again
      IQ(LEV+4) = IQ(LEV+4) + 1  

      CALL DZCHST('Check after 1',0,LEV,'LV',ISUM)
      PRINT '(''Value of \IQUEST(11)'',I5)', IQUEST(11)
            
*         Overwrite a link 

      LQ(LLVX(1)-1) = -1

C--     DZVERI will detect the error and send us to ZFATAL ('F' option)

      CALL DZVERI('Verify default division in //',2,'CFLSU')

      END</TT></PRE>
<P>
<P>
<PRE>


#
</PRE><P> <H2>References</H2>
<P><A NAME=BIBLIO HREF=../../general/biblio/bibliography.html>Global CERN library references</A><P>/user/goossens/cnasall/cnasbibl,/user/goossens/cnasall/textproc}    
<HR> <A NAME=tex2html15 HREF=tex2htmlindex2_2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/next_motif.gif"></A> <A NAME=tex2html13 HREF=ZEBDZ2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/up_motif.gif"></A> <A NAME=tex2html7 HREF=ZEBDZ2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/previous_motif.gif"></A>  <A NAME=tex2html17 HREF=tex2htmlindex2_2.html><IMG ALIGN=MIDDLE SRC="http://asis01.cern.ch/CN/CERNTEX/icons/index_motif.gif"></A><BR>
<B> Next:</B> <A NAME=tex2html16 HREF=tex2htmlindex2_2.html>Index</A>
<B>Up:</B> <A NAME=tex2html14 HREF=ZEBDZ2.html>No Title</A>
<B> Previous:</B> <A NAME=tex2html8 HREF=ZEBDZ2.html>No Title</A>
<HR> <P>
<HR>

</BODY>
<P><ADDRESS>
goossens@cern.ch
</ADDRESS>