macro post 1=p file=paw.post
********************************************************
* This macro has two parameters,                       *
* 1) the orientation or colour indicator (default=P)   *
* 2) the output file name [file] (default=paw.post)    *
********************************************************
fortran/file 66 [file]    | Open output file
1=$substring([1],1,1)     | First letter of first argument
1=$upper([1])                                           
if [1]=P goto por         | Portrait
if [1]=L goto lan         | Landscape
if [1]=C goto col         | Colour
if [1]<>G goto err        | Unknown option
meta -66 12201            | GKS metafile Postscript
    goto skip
por:
      meta -66 -111       | HIGZ portrait
      goto skip
lan:                      | HIGZ landscape
      meta -66 -112
      goto skip
col:                      | Simple metafile
      meta -66
skip:                    
      pic/plot ' '
      close 66            | close metafile
      if [1]=C goto gkscp
      shell /com/prf [file] -trans -pr lw | APOLLO printing only
      goto end
gkscp:
      shell gkscp         | shell script to plot on colour plotter
      goto end            
err:
      for/clo 66          | close metafile
      mess _
      'Usage: EXEC POST < [G]ks | [C]olour | [H]ori | [V]ert > < FILE=fname >'
end:
      return
