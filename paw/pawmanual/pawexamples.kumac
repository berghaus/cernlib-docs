****************************************************************
*                  FILE PAWEXAMPLES.KUMAC                      *
* This file contains all examples of the PAW tutorial chapter  *
* Last mod.  05 Oct 1989    mg                                 *
****************************************************************
MACRO ALLDEF 
****************************************************************
*                                                              *
* This MACRO initialises NICE values for producing PAW manual  *
*                                                              *
****************************************************************
opt *                   | Reset HPLOT options
set *                   | Reset picture dimensions
igset *                 | Reset HIGZ defaults
hi/de 0                 | Delete all histograms
ve/de *                 | Delete all vectors
viewing/zone 1 1        | Viewing zone is full screen
clr                     | Clear the picture
title_global ' '        | Blank global title
opt NBOX                | Do not draw a box around the picture
opt ZFL1                | Keep last picture in memory
set hwid 4              | Width of histogram lines
set fwid 4              | Width of fit lines
set bwid 4              | Width of box lines
set pwid 4              | Width of picture lines
igset lwid 4            | Width of  lines
set tfon -1042          | Font for text and title
set vfon -1042          | Font for axes
set gfon -1042          | Font for global title
set lfon -1042          | Font for axis labels
set cfon -1042          | Font for comment
set csiz 0.25           | Size for comments
set vsiz 0.25           | Size for axis values
set tsiz 0.32           | Size for comments
set xmgl 1.2            | X margin left
set xmgr 1.2            | X margin right
set ymgu 0.5            | Y margin left
set ymgl 1.5            | Y margin right
set gsiz 0.1            | Size for global title
set yhti 0.7            | Y position of histogram title
set htyp -3             | Hatch style (dotted - Postscript)
set ksiz 0.15           | Size for Hershey character 
igset mtyp 1            | Polymarker type (default)
RETURN

MACRO PAWEX1
****************************************************************
*                       PAW TUTORIAL EXAMPLE 1                 *
* Lines starting with a * are comments and are ignored by PAW  *
****************************************************************
             | Even so everything following a vertical bar is ignored 
             | so this character can be used to provide inline comments
vector/create VECT1(10)                    | Create vector VECT1 with 10 elements
vector/input VECT1 10 8 6 4 2 3 5 7 9 11   | Input the values of the 10 elements
             | ****  PAW commands are not case sensitive  ***
VECTOR/CREATE VX(20) R 1. 2. 3. 4. 5. 6. _ | Underscore _ is continuation character
7. 8. 9. 10. 11. 12. 13. 14. 15. 16. 17. 18. 19. 20.
VECTOR/CREATE VY(20) R 1.1 3.2 5.3 7.4 7.5 _
6.6 4.3 2.1 6.6 11.1 16.2 18.3 19.0 17.8 16.0 12.1 9.1 6.1 3.1 6.6
zon 1 2                                    | 2 pictures on the same page
vector/draw VECT1                          | Draw the contents of vector VECT1
GRAPH 20 VX VY                             | Graph VX and VY (defaults)
smk 21                                     | Set HIGZ polymarker
GRAPH 20 VX VY P                           | Graph VX and VY with polymarker
set DMOD 2                                 | Change line style
GRAPH 20 VX VY C                           | Graph VX and VY (using splines)
set DMOD 0                                 | Reset line style
VE/DE *                                    | Delete vectors
RETURN

MACRO PAWEX2
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 2                           *
* Example wshowing some more vector commands                             *
**************************************************************************
size 20 18                                  | Set picture size
vector/create VECT(10,3) R _                | Create a 2 dimensional vector VECT      
1. 2. 3. 4. 5. 6. 7. 8. 9. 10. _            | Underscore _ is continuation character
9.1 8.1 7.1 6.1 5.1 4.1 3.1 2.1 1.1 0.1 _ 
6.2 4.2 3.2 2.2 1.2 1.2 2.2 3.2 4.2 5.2
VECTOR/CREATE VECT1(10) R _                 | Create a 1 dimensional vector VECT1
1.1 2.2 3.3 4.4 5.5 6.6 5.5 4.4 3.3 2.2     | Input the values of the 10 elements
* PAW commands can be ABBREVIATED to their shortest non-ambiguous form
set htyp -3                                 | Define hatch style
ve/dr VECT(1:10,3)                          | Draw contents of second row on VECT2
set HTYP 0                                  | Define hatch style
set HWID 12                                 | Define histogram line width 
vector/draw VECT(1:10,3) ! SC               | Draw contents of VECT
set dmod 13                                 | 
igset MSCF 3.                               |
igset MTYP 29                               | Define marker type
set HWID 8                                  | Define histogram line width 
vector/draw VECT1 ! LPS                     | Draw contents of VECT1
* Notice the use of the EXCLAMATION MARK ! as a placeholder.
* It indicates that the default should be taken for the omitted parameter.
ve/list                                     | List vectors
ve/write VECT 'vector.data' '(3(10f5.0,/))' | Write VECT to coded output file
ve/delete *                                 | Delete all vectors in memory
RETURN 

MACRO PAWEX3
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 3                           *
* Example showing various possible data representation with VECTOR/DRAW  *
**************************************************************************
zone 2 3                                     | Divide picture page in 2 by 3 images
ve/create v(10) R 5 1 3 2 4 1 3 1 8 6        | Create demonstration vector V
ve/draw v                                    | default plot
ve/draw v ! b                                | plot as bar chart
ve/draw v ! l                                | plot as lines
ve/draw v ! l*                               | lines and a star
ve/draw v ! bl*                              | and bar option
igset mtyp 21                                | choose HIGZ marker
ve/draw v ! e                                | plot error bars
ve/de V                                      | delete vector V
zone                                         | Reset zone to default
RETURN

MACRO PAWEX4
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 4                           *
* Example which shows the difference between VECTOR/DRAW and VECTOR/PLOT *
* It also shows the functioanlity of VECTOR/HFILL and PUT/CONTENTS       *
**************************************************************************
zone 2 2
ve/create VECT1(10) R 1 2 3 4 5 5 4 3 2 1         | Create vector VECT1
set htyp -3
ve/draw VECT1                                     | Draw the contents of VECT1
* PLOT interprets contents of vector elements as values to be histogrammed
SET HCOL 1001
ve/plot VECT1                                     | PLOT contents into hist with 100 channels
set htyp -3
create/1dhisto 100 'test vector/hfill' 5 1. 6.    | Book a 1 dimensional histogram
MAX 100 2.5                                       | Define maximum for hist 100
ve/hfill VECT1 100                                | Fill hist 100 with vector values
histo/plot 100 'b'                                | Plot hist 100
hi/de 100                                         | Delete hist 100
create/1dhisto 100 'test put/contents' 10 1. 11.  | Book a 1 dimensional histogram
MAX 100 5.5                                       | Define maximum for hist 100
MIN 100 0.5                                       | Define minimum for hist 100
put/contents 100 VECT1                            | Fill contents of hist 100 with VECT1
histo/plot 100                                    | Plot hist 100 (identical to VECTOR/DRAW)
ve/de VECT1                                       | Delete VECT1
hi/de 100                                         | Delete hist 100
zone                                              | Reset zone
RETURN

MACRO PAWEX5
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 5                          *
* Example showing vector operations. The resulting vectors are created   *
* automatically.                                                         *
**************************************************************************
zone 1 2
ve/create V1(10) R 1 2 3 4 5 5 4 3 2 1            | Define vector VECT1 of 10 elements
vector/operations/vscale V1 0.5   V12             | Divide V1 by 2 into V12
ve/op/vscale             V1 0.25  V14             | Divide V1 by 4 into V14
set htyp 0                                        | No hatching  
*   Differences between various plots are shown using different line styles
ve/dr V1                                          | Draw V1
ve/dr V12 ! S                                     | Draw V12 onto same graph
ve/dr V14 ! S                                     |     ditto for V14
vsub                      V1 V14 V14M             | Substract V14 from V1 into V12M
*   Differences between various plots are shown using different hatch styles
ve/dr V1                                          | Draw V1 
set htyp 344                                      | Change hatch style
ve/dr V14M ! S                                    | Draw V14M on same graph
set htyp 144                                      | Change hatch style once more
ve/dr V12  ! S                                    | Draw V12 on same graph
ve/de *                                           | Get rid of vectors
zone                                              | Reset zone
RETURN

MACRO PAWEX6
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 6                           *
* Example showing a simple macro, with a loop and a VECTOR fit.          *
**************************************************************************
ve/create VECT(10,3)                                  | Create a 2 dimensional vector
ve/read VECT 'vector.data'                            | Read vector values on external file
                                                      | created in example PAWEX2)
ve/print VECT(1:10,3)                                 | Print last row of VECT (compare with Example 2)
vbias vect(1:10,1) 0.5 vect(1:10,1)
zon 1 2
ip = 2                                                | Loop counter
set htyp -3
* Start of a loop over the three rows of vector VECT
LOOP:                                                 |<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
  ve/draw VECT(1:10,[ip])                             | draw next row                    ^
  order = ip - 1                                      | Define order for fit             ^
  ve/fit VECT(1:10,1) VECT(1:10,[IP]) ! P[order] WS   | Fit polynomial to given row      ^
  ip = ip + 1                                         | increment counter                ^
  if ip < 4        GOTO LOOP                          | loop test >>>>>>>>>>>>>>>>>>>>>>>>
ve/delete VECT                                        | Delete VECT
RETURN                                         

MACRO PAWEX7
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 7                           *
* Plot a few one-dimensional functions                                   *
**************************************************************************
opt utit                                              | No title on histogram
opt grid
func/plot x*sin(x)*exp(-0.1*x)  -10. 10.              | Plot first function
set dmod 2
func/plot (sin(x)+cos(x))**5    -10. 10. s            | Plot second function on same plot
set dmod 3
func/plot (sin(x)/(x)-x*cos(x)) -10. 10. s            | Plot third function on same plot
set dmod 1
opt htit                                              | No title on histogram
opt ngri

RETURN

MACRO PAWEX8  1=8
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 8                           *
* Plot a one-dimensional function and loop                               *
* The Macro parameter is the number of plot to draw on the picture       *
* The defaults is 8                                                      *
**************************************************************************
SET DMOD 1                                            | Do not change line style 
SET XTIC 0.0001                                       | No tickmarks in X
SET YTIC 0.0001                                       | No tickmarks in Y
SET Xval 100.                                         | No values on Y axis
SET Yval 100.                                         | No values on X axis
OPT utit                                              | No title on histogram
FUN/PLOT X*SIN(X) -10 10                              | Plot first function
FUN/PLOT X*COS(X)*SIN(X) -10 10 S                     | Plot second function
A=[1]-1                                               | Initialise loop variable
LOOP:                                                 | Start of loop <<<<<<<<<
  FUN/PLOT X*SIN(X)*[A]/[1] -10 10 S                  | Next plot             !
  FUN/PLOT X*COS(X)*SIN(X)*[A]/[1] -10 10 S           | Next plot             !
A=[A]-1                                               | Update loop variable  !
IF [A]>0 GOTO LOOP                                    | End of loop >>>>>>>>>>>
opt htit                                              | Back to HBOOK titles
RETURN

MACRO PAWEX9
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 9                           *
* Plot two-dimensional functions                                         *
**************************************************************************
zone 2 2                                              | Divide picture in two by two
FUN2  10 abs(sin(x)/x)*(cos(y)*y) 40 -6.28 6.28 _     | First 2-dimensional function as
40 -6.28 6.28                                         | surface plot representation (default)
contour 10 40 0                                       | Contour plot representation 
FUN2  10 x*sin(x)*y*sin(y) 40 -10. 10. 40 -10. 10. C  | Second 2-dim function (contour)
surface 10                                            | Surface plot representation 
hi/de 10                                              | Delete histogram 10
RETURN

MACRO PAWEX10 1=100
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 10                          *
* Calculate and plot (BOX option) Mandel distribution                    *
**************************************************************************
fun2 10 mandel.ftn [1] -2.4 .8 [1] -1.2 1.2 
*      REAL FUNCTION MANDEL(XP)
*      DIMENSION XP(2)
*      DATA NMAX/30/
*      X=XP(1)
*      Y=XP(2)
*      XX=0.
*      YY=0.
*            DO 30 N=1,NMAX
*               TT=XX*XX-YY*YY+X
*               YY=2.*XX*YY+Y
*               XX=TT
*               IF (4..LT.XX*XX+YY*YY) GO TO 1
*   30       CONTINUE   
*    1       MANDEL=FLOAT(N)/FLOAT(NMAX)
*      END
hi/pl 10 BOX
RETURN

MACRO PAWEX11
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 11                          *
* Creation of one and two dimensional histograms                         *
**************************************************************************
OPT GRID
zon 1 2
function/fun1 100 htfun1.ftn 100. 0. 1.               | Fill 1-dim histogram 100 with 2 gaussians
histogram/create/1dhisto  110 'Test 1-dim Histo' 100 0. 1. 1000.
call urout.ftn(5000)                                  | Fill histogram 110 according to function 100
fun/fun2 200 htfun2 25. 0. 1. 25. 0. 1. C             | Fill 2-dim histogram 200 with 2 gaussians
hi/li                                                 | List known histograms
histogram/file 1 pawhists.rzdat 1024 N                | define output unit 1 
hrout 0                                               | Write all histograms
close 1                                               | Close the output unit
hi/de 0                                               | Delete all histograms
RETURN

MACRO PAWEX12
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 12                          *
* Read histograms from file and plot                                     *
**************************************************************************
histogram/file 1 pawhists.rzdat                       | Open histogram file
hrin  *                                               | Read all histograms on file
ldir                                                  | List current directory
opt grid                                              | Put a grid on the picture
hi/list                                               | List known histograms
zon 2 2                                               | Divide picture in two by two parts
hi/pl 100                                             | Plot histogram 100
set htyp -3
hi/pl 110                                             | Plot histogram 110 with option E
zone 1 2 2 'S'                                        | Redefine zone
hi/plot 200                                           | Plot 2-dim histogram 2 (Box option)
h/del 0                                               | Delete histograms from memory
close 1                                               | Close input unit
zone                                                  | Reset zone
RETURN

MACRO PAWEX13
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 13                          *
* Perform operations on histograms read from file and save result        *
**************************************************************************
histogram/file 1 pawhists.rzdat ! U                   | Open histogram file
hrin  0                                               | Read all histograms on file
zon 2 2                                               | Divide picture in two by two parts
opt grid                                              | Put a grid on the picture
igset mtyp 26                                         | Polymarker
set KSIZ 0.2                                          | Set size of polymarker (in cm)
hi/pl 110 e                                           | Plot histo 110 with E option
hi/pl 110 pl                                          | Plot histo 110 with polymarker 
set CFON 2                                            | Set font for comments
set CSIZ 0.4                                          | Set size for comments (in cm)
key 0.5 130 26 '[p^+!,p^-!,m^+!,m^-'                  | Example of HIGZ software fonts
zon 1 2 2 s                                           | Redefine picture layout
hi/op/add 110 110 120 0.5 0.                          | Histogram 120 is half of 110
hi/op/add 110 110 130 0.25 0.                         | Histogram 130 is quarter of 110
set htyp -3                                           | Hatch style for histogram
hi/pl 110                                             | Plot histogram 110
set htyp 444                                          | Set hatch styles for histogram 120
hi/pl 120 s                                           | Plot histogram 120 on same plot
set htyp 244                                          | Set hatch styles for histogram 130
hi/pl 130 s                                           | Plot histogram 120 on same plot
text 0.55 95. 'LEP Very Preliminary' 0.35 25.         | Add text to plot
hrout 0                                               | Write all histograms to the histogram file
close 1                                               | Close input/output unit
RETURN

MACRO PAWEX14
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 14                          *
* Different representations of 2-dim histograms                          *
**************************************************************************
histogram/file 1 /user/goossens/paw/pawhists.rzdat                       | Open histogram file
hrin  0                                               | Read all histograms on file
hi/li                                                 | List histograms on file
close 1                                               | Close input unit
opt UTIT                                              | Turn off HBOOK titles
zon 2 2                                               | Divide picture in two by two parts
set htyp 0
hi/pl 200 BOX                                         | Plot histogram 200 with BOX option
hi/2d/con 200 20 0   | as a contour plot                   | Plot histogram 200 as contour plot
igset lwid 1
hi/2d/lego 200  | as a lego plot                      | Plot histogram 200 as lego plot
hi/2d/sur 200   | as a surface plot                   | Plot histogram 200 as surface plot
hi/del 0                                              | Delete histograms in memory
zon                                                   | Reset zone
RETURN

MACRO PAWEX15
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 15                          *
* Use of subranges in histogram specifiers                               *
**************************************************************************
histogram/file 1 /user/goossens/paw/pawhists.rzdat                       | Open histogram file
hrin  0                                               | Read all histograms on file
close 1                                               | Close input unit
opt GRID                                              | Draw grid on picture
opt UTIT                                              | Turn off HBOOK titles
zon 2 2                                               | Divide picture in two by two parts
hi/pl 110(56:95) e                                    | Plot subrange of histogram 110
opt NGRI                                              | No grid on picture
set htyp -3                                           | Hatch style for histogram
hi/pl 200(8:8,)     BOX                               | Plot subrange of 200 with BOX option
hi/pl 200(3:15,3:15) CONT                             | Plot subrange of 200 as a CONTOUR plot
hi/pl 200(4:12,4:12) LEGO                             | Plot subrange of 200 as a SURF plot 
hi/del 0                                              | Delete histograms in memory
zon                                                   | Reset zone
RETURN

MACRO PAWEX16
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 16                          *
* Creation of an Ntuple and first look at its contents                   *
**************************************************************************
Ntuple/create 10 'CERN Population' 11 ' ' 3500  _     | Create Ntuple 10 with 11 variables
Category Division Flag Age Service Children Grade Step Nation Hrweek Cost
Ntuple/read 10 aptuple.dat                            | Read the elements of the ntuple
Histo/file 1 aptuple.rzdat 1024 N                     | Open Histogram file on unit 1
hrout 10                                              | Write the ntuple to the file
ntuple/print 10                                       | Look what Ntuple 10 contains
zone 1 2                                              | Divide the plot into two
opt stat                                              | Print statistics on plot
set stat 110                                          | Number entries and average
set htyp -3
Ntuple/plot 10.Age                                    | Plot Age distribution
Ntuple/plot 10.Cost                                   | Cost distribution
Close 1                                               | Close file 1
zone                                                  | Reset zone 
hi/de 10                                              | Delete histogram 10 from memory
RETURN                  

MACRO PAWEX17
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 17                          *
* Read an Ntuple from a histogram file                                   *
* Difference between automatic and user binning                          *
**************************************************************************
hi/file 2 'aptuple.rzdat'                               | Open histogram file
zon 2 2                                                 | Divide plot into two by two squares
opt NSTA
opt GRID
set XTIC 0.15                                           | X-axis tick mark length
set YTIC 0.15                                           | Y-axis tick mark length
set htyp -3
Ntuple/pl 10.age                                 | Age distribution with automatic binning
hi/create/1dhisto 11 'Age - User binning' 45 20. 65.    | Book histogram 11 
set ndvx -509                                           | We want 5 secondary and 9 primary divisions
Ntuple/project 11 10.Age                         | Age distribution with user binning
hi/plot 11                                              | Plot histogram 11
hi/create/1dhisto 12 'Cost - User binning' 50 0. 20000. | Book histogram 12 
set ndvx
Ntuple/plot 10.cost                              | Cost distribution with automatic binning
set ndvx -504                                           | 5 secondary and 4 primary divisions 
Ntuple/pl 10.Cost ! -12                          | Cost distribution with user binning
close 2                                                 | Close histogram file
zone                                                    | Reset zone 
hi/de 0                                                 | Delete histograms from memory

RETURN                  

MACRO PAWEX18
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 18                          *
* Ntuple SCAN and the use of simple selection criteria                   *
**************************************************************************
hi/file 2 'aptuple.rzdat'                               | Open histogram file
opt grid
opt stat                                                | Print statistics on plot
set stat 110                                            | Indicate number of entries and average
ALIAS/CREATE DIVEP 5                                    | Create alias for EP Division
ALIAS/CREATE NATFR 7                                    | Create alias for French nationality
cd //PAWC                                               | Set current directory to memory
NT/SCAN //LUN2/10 nation=NATFR.and.division=DIVEP  _    | Scan and look for French CERN staff in EP division
! ! 5 age service children grade step                   |   printing the variables indicated
hi/create/1d 200 'Number of years at CERN' 35 0. 35.    | Create histogram 200
max 200 250                                             | Maximum for histogram 200
set ndvx 507                                            | We want 5 secondary and 7 primary divisions
set htyp -3                                             | Hatch at 45 degrees left
Nt/pl //LUN2/10.Service ! -200                          | Plot years of service
ATITLE 'Years at CERN' 'Number of staff'                | Define axis titles
set htyp 344                                            | Hatch at 45 degrees left
Nt/pl //LUN2/10.Service nation=NATFR -200 ! ! S         | Number of years at CERN for French staff members
set htyp 144                                            | Hatch at 45 degrees right
Nt/pl //LUN2/10.Service _
division=DIVEP.and.nation=NATFR -200 ! ! S              | Number of years at CERN for French Staff in EP
close 2                                                 | Close histogram file
set htyp 0                                              | Reset hatch style
hi/de 0                                                 | Delete histograms from memory
zone                                                    | Reset zone 
RETURN                  

MACRO PAWEX19
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 19                          *
*  Use of Ntuple masks and loop construct                                *
**************************************************************************
hi/file 2 'aptuple.rzdat'                               | Open histogram file
hrin 0                                                  | Read Ntuple into memory
close 2                                                 | Close histogram file
set xtic 0.0001                                         | Make tick marks invisible
1dhisto 20 'Distribution by grade' 12 3 15              | Create histogram 20
opt stat                                                | Print statistics on plot
SET STAT 101                                            |  show only number of entries on plot
opt grid                                                | Grid on
set grid 1003                                           | Only horizontal grid with line type equal 3
opt bar                                                 | Activate option bar
igset barw 0.8                                          | Width of bars
igset baro 0.1                                          | Origin of bar
max 20 700                                              | Maximum for histogram 20
set NDVX -12.05                                         | Exactly 12 divisions with text centered in X
set NDVY -507                                           | We want 5 secondary and 7 primary divisions in Y 
set htyp -3                                             | Dotted hatch style (PostScript)
Ntuple/plot 10.grade ! -20                              | Plot grade distribution into histogram 10
box 9.5 10.5 610 640                                    | Draw a box on the histogram
igset TXAL 13                                           | Left adjust X and center Y
igset TXFP -130                                         | Roman font
igset CHHE 0.35                                         | Character height
itx 10.7 625 'All Staff'                                | Add some text to the histogram
set htyp 244                                            | Change the hatch style
*  Define via a mask those people which are at the end of their grade
*ntuple/mask stmask N 3500                               | Define mask
*ntuple/loop 10.grade step=15>>stmask(1)
*ntuple/loop 10.grade grade>4.and.step=13>>stmask(2)
*ntuple/loop 10.grade (grade=13.and.step=10).or.(grade=14.and.step=7)>>stmask(3)
*Ntuple/plot 10.grade stmask(1).or.stmask(2).or.stmask(3)>>stmask(4) -20 ! ! S
*ntuple/mask stmask C                                    | Close mask file
ntuple/mask stmask                                      | Define mask
ntuple/loop 10.grade stmask(1)
ntuple/loop 10.grade stmask(2)
ntuple/loop 10.grade stmask(3)
Ntuple/plot 10.grade stmask(4) -20 ! ! S
ntuple/mask stmask C                                    | Close mask file
box 9.5 10.5 560 590                                    | Draw aother box on the histogram
itx 10.7 575 'Staff at end of grade'                    | Add some more text to the histogram
ATITLE 'Grade' 'Number of Staff'                        | Axis title
set htyp 0                                              | Reset hatch style
hi/de 0                                                 | Delete histograms from memory
RETURN                  

MACRO PAWEX20
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 20                          *
* The use of Ntuple Cuts                                                 *
**************************************************************************
hi/file 2 'aptuple.rzdat'                               | Open histogram file
Ntuple/cut 1 MOD(FLAG,2).EQ.0                           | CUT 1 : Female staff 
Ntuple/cut 2 MOD(FLAG,4)>1                              | CUT 2 : Non-resident staff 
1d 20 'Male/female and resident/non-resident Staff' 13 1 14   
opt grid                                                | Activate grid on plot
set grid 1003                                           | Only horizontal grid with line type equal 3
opt stat                                                | Print statistics on plot
SET STAT 10                                             |  show only number of entries on plot
set xtic 0.0001                                         | Eliminate tick marks
opt bar                                                 | Activate the bar chart option
igset barw 0.4                                          | Set bar origin
igset baro 0.1                                          | Set bar width
max 20 600                                              | Maximum for histogram 20
LABELS 1 13 AG DD DG EF EP FI LEP PE PS SPS ST TH TIS   | Define labels for X axis
set NDVX 13.15                                          | X axis should centre the labels and plot them down
set NDVY -506                                           | We want 5 secondary and 7 primary divisions in Y 
Ntuple/plot 10.division ! -20                           | Plot all staff per division into histogram 20
set htyp -3                                             | Change hatch style
Ntuple/plot 10.division 2 -20 ! ! s                     | Plot staff per division (satisfying cut 2)
igset baro 0.5                                          | Reset bar origin
set htyp 145                                            | Change hatch style
Ntuple/plot 10.division 1 -20 ! ! s                     | Plot staff per division (satisfying cut 1)
set htyp 154                                            | Change hatch style
Ntuple/plot 10.division 1.and.2 -20 ! ! s               | Plot staff per division (satisfying cuts 1 and 2)
ATITLE 'Division' 'Number of staff'                     | Axis title
close 2                                                 | Close histogram file
set htyp 0                                              | Reset hatch style
hi/de 0                                                 | Delete histograms from memory
RETURN                  

MACRO PAWEX21
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 21                          *
* Two dimensional Ntuple distributions                                   *
**************************************************************************
hi/file 2 'aptuple.rzdat'                               | Open histogram file
clr                                                     | Clear screen
2d 20 ' ' 12 3 15 16 0 16 0.                            | Book 2-dim histogram 20
Nt/project 20 //LUN2/10.step%grade                      | Project Ntuple onto histogram 20
lego 20(1:,7:) 20 40                                    | Plot resulting histogram 20 as LEGO plot
close 2                                                 | Close histogram file
hi/de 0                                                 | Delete histograms from memory

RETURN          

MACRO PAWEX22
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 22                          *
* Examples of the SIGMA processor 1                                      *
**************************************************************************
zone 2 2                             | Divide picture two by two
opt GRID                             | Draw grid on picture
sigma x=array(200,0#2*PI)            | Define array x with 200 elements values between 0 and 2 pi
sigma sinus=sin(x)                   | sinus is array defined implicitly
sigma sinx=sin(x)/x                  | sinx       ditto
gra 200 x sinus                      | Make graph of array sinus
set dmod 2                           | New line style
gra 200 x sinx l                     | Make graph of array sinx
set dmod 0                           | Default line style
SIGMA x=array(300,0#8)               | Define array x with 300 elements filled with values between 0 to 8
SIGMA g=cosh(x)+sin(1/(.1+X*X))      | Hyperbolic cosine oscillations are invisible
gra 300 x g                          | Plot the vectors x and g
SIGMA  x=array(300,0#3)              | Define array x with 300 elements filled with values between 0 to 3
SIGMA g=cosh(x)+sin(1/(.1+X*X))      | Calculate the ordinate
gra 300 x g                          | Plot the vectors x and g
SIGMA  x=array(300,0#1)              | Define array x with 300 elements filled with values between 0 to 1
SIGMA g=cosh(x)+sin(1/(.1+X*X))      | Calculate the ordinate
gra 300 x g                          | Plot vectors x against g
RETURN 

MACRO PAWEX23
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 23                          *
* Examples of the SIGMA processor 2                                      *
**************************************************************************
zone 2 2                             | Divide picture two by two
sigma x=array(200,0#5)               | Define array x with 200 elements filled with values between 0 to 5
sigma A=8                            | Assign scalar A
sigma B=.01                          | Assign scalar B
sigma Y=EXP(-X)*SIN(A*X)+B*X*X       | Y is an array depending an A and B
opt GRID                             | Draw grid on picture
gra 200 x y                          | 2-dim representation x / y
opt NGRI                             | Plot no more grid on picture
SIGMA x=array(200,0#2*pi)            | Array between 0 and 2 pi
SIGMA s=sin(x)                       | Define some more arrays (implicit creation of array s)
SIGMA S2=S/2                         |                         (implicit creation of array s2)
SIGMA c=cos(x)                       |                         (implicit creation of array c)
SIGMA c2=c/2                         |                         (implicit creation of array c2)
SIGMA s4=s/4                         |                         (implicit creation of array s4)
SIGMA c4=c/4                         |                         (implicit creation of array c4)
gra 200 s c                          | Then plot all arrays in same plot
gra 200 s2 c l                       |           ditto
gra 200 s4 c l                       |           ditto
gra 200 s c2 l                       |           ditto
gra 200 s2 c2 l                      |           ditto
gra 200 s4 c2 l                      |           ditto
gra 200 s c4 l                       |           ditto
gra 200 s2 c4 l                      |           ditto
gra 200 s4 c4 l                      |           ditto
sigma a=array(100,0#59.77)           | A array of 100 elements with values between 0 and 59.77 (19*pi)
SIGMA NC=NCO(A)                      | interrogate SIGMA to know array length
SIGMA y=cos(a)*a                     | y becomes an array of length 100
SIGMA x=sin(a)*a                     | x becomes an array of length 100
GRA NC X Y                           | Plot x versus y
SIGMA a=a*2.55555                    | Increase the amplitude
SIGMA y=cos(a)*a                     | y becomes an array of length 100
SIGMA x=sin(a)*a                     | x becomes an array of length 100
GRA NC X Y                           | Plot x versus y
RETURN          

MACRO PAWEX24
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 24                          *
* Operations on histograms (Keep and Update)                             *
**************************************************************************
histogram/file 1 pawhists.rzdat                       | Open histogram file
Zone 1 2                                              | Divide plot in 2 vertically
opt grid                                              | Draw grid on picture
set grid 1003                                         | Horizontal grid only
set hcol 1005
h/pl 120 k                                            | Plot hist 120 and keep in memory
set grid                                              | Default grid (horizontal + vertical)
set htyp -3
h/pl 110                                              | Plot histogram 120
set hcol 1002
h/pl 110 +                                            | Add 110 on the last kept Histogram
set htyp 144
hi/pl 130 +                                            | Add 130 on the last kept Histogram
zone                                                  | Reset zone 
close 1                                               | Close input unit
RETURN          

MACRO PAWEX25
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 25                          *
* Merge pictures onto one plot                                           *
**************************************************************************
histogram/file 1 pawhists.rzdat                       | Open histogram file
EXEC PICTMERGE hid=110 s1=31 s2=40                    | Plot histo 110 with subrange as inset
close 1                                               | Close input unit
RETURN          
Macro PICTMERGE HID=0 S1=1 S2=100
**************************************************************************
*                   PAW TUTORIAL EXAMPLE 25 - MERGE Macro                *
**************************************************************************
Next                                                  | Initialise next picture
Set *                                                 | Reset Graphics settings
Opt *                                                 | Reset options
opt NBOX                                              | No box around picture
set hwid 4                                            | Width of histogram lines
set bwid 4                                            | Width of box lines
set pwid 4                                            | Width of picture lines
igset lwid 4                                          | Width of  lines
set tfon -1042                                        | Font for text and title
set vfon -1042                                        | Font for axes
set gfon -1042                                        | Font for global title
set lfon -1042                                        | Font for axis labels
set cfon -1042                                        | Font for comment
PI/del *                                              | Delete all pictures from memory
Swi Z                                                 | Z mode only
Pic/cr MERGE2                                         | Create a picture with name MERGE2
set HTYP -3                                           | Set hatch type -3 
Hi/PL [hid]                                           | Plot histo HISTID into picture MERGE2
set htyp 244                                          | Set histo hatch style 
Hi/PL [hid]([s1]:[s2]) s                              | Hatch sub-range of histo HISTID
Pic/cr MERGE1                                         | Create next picture
set HTYP -3                                           | Reset hatch style
opt utit                                              | No histogram title
opt grid
Hi/PL [hid]([s1]:[s2])                                | Plot the subrange into picture MERGE1
opt ngri
Izpict MERGE2 c                                       | Make MERGE2 current picture
Pi/Merge MERGE1 .45 .45 .47 D                         | Merge picture MERGE1 with current picture 
Swi G                                                 | G mode only
Return

MACRO PAWEX26
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 26                          *
* Make a complex graph with PAW using Postscript fonts                   *                                          *
**************************************************************************
EXEC COMPTIME
RETURN
MACRO COMPTIME
**************************************************************************
*             Companion macro to PAW TUTORIAL EXAMPLE 26                 *
**************************************************************************
OPT *                                                           | Reset options
SET *                                                           | Reset HPLOT settings
igset *                                                         | Reset HIGZ settings
ve/de *                                                         | Delete all vectors
OPT NBOX                                                        | No box on plot
OPT LOGY                                                        | Logaritmic plot in y
OPT TIC                                                         | Tic marks
OPT UTIT                                                        | User title
opt ZFL1                                                        | Keep picture in memory
size 16 20                                                      | Size of picture
set hwid 4                                                      | Width of histogram lines
set fwid 4                                                      | Width of fit lines
set bwid 4                                                      | Width of box lines
set pwid 4                                                      | Width of picture lines
igset lwid 4                                                    | Width of  lines
set VSIZ 0.20                                                   | Size of axis values smaller
set YGTI 1.2                                                    | Y position of global title
set XVAL 0.4                                                    | Distance y axis to axis values
set YVAL 0.2                                                    | Distance x axis to axis values
set XLAB 1.0                                                    | Distance y axis to label
set YLAB 1.2                                                    | Distance x axis to label
set XTIC 0.15                                                   | Length x axis tick marks
set YTIC 0.15                                                   | Length y axis tick marks
set ASIZ 0.26                                                   | Size of axis text
set GSIZ 0.35                                                   | Global title size
set gfon -60                                                    | Global title font
set vfon -60                                                    | Font of axis values
set lfon -10                                                    | Font of axis labels
igset lwid 4                                                    | Qaudruple line width
title_gl 'CERN Central Computer Usage'                          | Global title of picture
vector/create vy(30) R 9.2 11.8 34.9 60.7 87.1 217.8 360 1250 2500 4006 _  | IBM168 hours used for each year since 1960
  4478 5590 5910 6246 10879 12849 18429 19481 21171 25005 _
  31219 33928 37057 45520 57000 75957 98806 118993 131800 151138    
sigma vx=array(30,60#89)                                        | Define vector of x values 60 to 89
ve/cre f1(2) r 2*0.0                                            | Create vector V1 for fit 1 result
ve/cre f2(2) r 2*0.0                                            | Create vector v2 for fit 2 result
SET NDVX -30.05                                                 | Exactly 30 divisions in X with labels centered on divisions
NULL 60 90 5 250000                                             | Draw empty plot with given limits
igset MSCF 0.75                                                 | Specify marker scale factor
igset mtyp 21                                                   | Data points with polymarker
graph 30 vx vy p                                                | Plot data points 
ve/fi vx(:10) vy(:10) ! e ws ! f1                               | Fit first part of data points
ve/fi vx(10:) vy(10:) ! e ws ! f2                               | Fit second part of data points
arrow  64. 62.    10.    10.  0.15                              | Draw arrow
igset txal 20                                                   | Center text horizontally
igset chhe 0.18                                                 | Character height
itx 63.    12. 'IBM 709'                                        | Write text
arrow  65. 63.    35.    35. -0.11                              | Draw arrow
itx 64.    40  'IBM 7090'                                       | Write text
arrow 75. 65.    230.   230. -0.11                              | Draw arrow
itx 70.   260. 'CDC 6600'                                       | Write text
arrow 85. 72.   4000.  4000. -0.11                              | Draw arrow
itx 78.5  4500. 'CDC 7600'                                      | Write text
arrow 82. 78.   6500.  6500. -0.11                              | Draw arrow
itx 80.   7500. 'IBM 168 '                                      | Write text
arrow 81. 79.  10000. 10000. -0.11                              | Draw arrow
itx 80.  12000. 'IBM 3032'                                      | Write text
arrow 85. 81.  18000. 18000. -0.11                              | Draw arrow
itx 83.  20000. 'IBM 3081'                                      | Write text
igset txal 10                                                   | Left align text
arrow 84. 82.  27000. 27000. -0.11                              | Draw arrow
itx 82.  30000. 'SIEMENS 7880'                                  | Write text
igset txal 20                                                   | Center text horizontally
arrow 90. 84.  42000. 42000.  0.11                              | Draw arrow
itx 87.  50000. 'SIEMENS 7890'                                  | Write text
arrow 90. 85.  68000. 68000.  0.11                              | Draw arrow
itx 87.5 72000. 'IBM 3090'                                      | Write text
arrow 90. 88.  100000. 100000.  0.11                            | Draw arrow
itx 89. 110000. 'CRAY'                                          | Write text

arise=$sigma(int((exp(f1(2))-1)*100+0.5))//'% per Annum rise'   | Get slope of annual increase from fit 1
xhand=68.                                                       | X coordinate for hand and text
yhand=$sigma(exp(f1(1)+f1(2)*[xhand]))                          | Calcualte y coordinate from fit 1
EXEC DRAW X=[xhand] Y=[yhand] TEXT=[arise]                      | Draw the hand and text
arise=$sigma(int((exp(f2(2))-1)*100+0.5))//'% per Annum rise'   | Get slope of annual increase from fit 2
xhand=84.                                                       | X coordinate for hand and text
yhand=$sigma(exp(f2(1)+f2(2)*[xhand]))                          | Calcualte y coordinate from fit 2
EXEC DRAW X=[xhand] Y=[yhand] TEXT=[arise]                      | Draw the hand and text
atitle 'Year ' 'IBM 168 Units used '                            | Add axis title to picture
RETURN

MACRO DRAW
igset TXAL 35                                                   | Right horizontally and bottom vertically
igset TANG -35.                                                 | Text angle       
igset TXFP -140                                                 | Zapfdinbats font
igset CHHE  0.50                                                | Character height
itx $SIGMA([X]-0.9) [Y] +                                       | A hand in the font ZapfDingbats
igset TXAL 30                                                   | right align text
igset TANG  0.                                                  | Text angle       
igset TXFP -30                                                  | Font for accompanying text
igset CHHE  0.22                                                | Character height
y = y * 1.70                                                    | Start text somewhat above hand
itx [X] [Y] [TEXT]                                              | The text to be written
RETURN

MACRO PAWEX27
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 27                          *
* Pie chart and Bar chart                                                *
**************************************************************************
EXEC ALLDEF
EXEC PIE
RETURN
MACRO PIE
**************************************************************************
*             Companion macro to PAW TUTORIAL EXAMPLE 27                 *
**************************************************************************
alias/cre colbackg  0                                 | background colour
alias/cre colcompl  1                                 | complement of background
alias/cre colred    2                                 | red
alias/cre colgreen  3                                 | green
alias/cre colblue   4                                 | blue
alias/cre colyellow 5                                 | yellow
alias/cre colpurple 6                                 | purple
alias/cre colcyan   7                                 | cyan
igset bord 1
zon 2 2
null 0 20 0 20 a

v/cre vws(5) R 28.3 18.6 16.9 13.5 22.7
label 1 5 'Sun' 'DEC' 'HP' 'Apollo' 'Other'

v/cre offset(5)  R 2*0. 2*20. 0.
v/cre colour(5)  R colred colgreen colblue colyellow colpurple 
v/cre style(5)   R 111 222 333 444 265
                  
igset fais 1
pie 10. 10. 7.  5 vws p offset ! colour
null 0 20 0 20 a
igset fais 1
pie 10. 10. 7.  5 vws l offset ! colour
null 0 20 0 20 a
pie 10. 10. 7.  5 vws s offset style
null 0 20 0 20 a
pie 10. 10. 7.  5 vws l offset style
al/de *
RETURN

MACRO PAWEX30
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30                          *
* Making slides with PAW and HIGZ                                        *
**************************************************************************
alias/create mainfont -60                                  | Main font and precision (HELVETICA BOLD)
* Define alias to print Postscript metafile (See <HDREF REFID=H3PICT> where macro POST is described)
alias/create @ 'exec post'           
opt zfl1                                                   | Keep last picture in memory
exec discomp                                               | Exec the slide macro containing the slide
*@                                                          | Print generated slide
alias/delete *                                             | Delete Alias definition

RETURN          
MACRO DISCOMP
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30 - DISCOMP                *
* Text for slide example DISCOMP                                         *
**************************************************************************
exec slide sn='DisComp' title='Distributed Computing'      | Define the outline of the slide
igset txfp mainfont                                        | Defined font via alias
igset chhe 0.5                                             | set character height
itx 2 17 'With a distributed operating system (not yet !)' | Write text
itx 2 15 'With tools on top (RPCs, NCS,.. ?)'              | Write text
igset chhe 0.4                                             | Set character height
itx 3 14 Tmess                                             | Write text
itx 3 13 Tfork                                             | Write text
itx 3 12 Tdata                                             | Write text
itx 3 11 Tcomp                                             | Write text
igset txfp -70                                             | Defined new font (Helvetica-bold-oblique)
itx 5 14 'Time to send message to remote process'          | Write text
itx 5 13 'Time to fork a process'                          | Write text
itx 5 12 'Time to pass data (in and out)'                  | Write text
itx 5 11 'Time used for computation on remote process'     | Write text
igset txfp mainfont                                        | Defined font via alias
pave 2 16 2 9 0.3 0 1001 trs                               | Draw 'pave' (shaded box)
igset txal 33                                              | Text alignment Hor-Right / Vert-Centered
itx 6 7 'Efficiency ='                                     | Write text
igset txal 20                                              | Text alignment Hor-Centered / Vert-Base
line 6.1 7 14.1 7                                          | Draw line
itx 10 7.2 Tcomp                                           | Write text
itx 10 6.3 'Tcomp + Tmess + Tfork + Tdata'                 | Write text
igset txfp -240                                            | Defined new font (ZapfDingbats-bold)
igset chhe 0.6                                             | Set character height
igset txal 30                                              | Text alignment Hor-Right / Vert-Base
itx 1.5 17 P                                               | Write text
itx 1.5 15 P                                               | Write text
igset chhe 0.3                                             | Set character height
igset txal 20                                              | Text alignment Hor-Centered / Vert-Base
igset txfp -70                                             | Defined new font (Helvetica-bold-oblique)
itx 9 4 'Many time consuming applications today have:'     | Write textOn apollo execute 
itx 9 3 'Efficiency > 0.9'                                 | Write text

RETURN          

macro SLIDE xsize=18 ysize=22 width=0.4 name='Author/CERN  CONF99' sn=' ' title=' '
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30 - SLIDE                  *
* General macro to draw contours of SLIDE                                *
**************************************************************************
xmax = [xsize]-[width]                                     | X range for slide
ymax = [ysize]-[width]                                     | Y range for slide
size [xsize] [ysize]                                       | Total size for slide
next                                                       | Initialize next frame
igset *                                                    | Reset HIGZ settings
igset lwid 2                                               | Double line width
pave 0 [xmax] 0 [ymax] [width] 0 1005 tr                   | Draw a shaded box around the picture
igset lwid 1                                               | Single line width
xtitle = $sigma(([xsize]-0.2)/2.)                          | X coordinate for title
ytitle = [ysize]-1.5                                       | Y coordinate for title
igset txfp -70                                             | Defined new font (Helvetica-bold-oblique)
igset txal 20                                              | Text alignment Hor-Centered / Vert-Base
igset chhe 0.6                                             | Set character height
itx [xtitle] [ytitle] [title]                              | Write text (title)
igset chhe 0.3                                             | Set character height
igset txal 10                                              | Text alignment Hor-Left / Vert-Base
xtext = [xmax]-0.2                                         | X coordinate for name
ytext = 0.1                                                | Y coordinate for name
igset txfp -1042                                           | Defined new font (Helvetica-bold-oblique)
igset chhe 0.2                                             | Set character height
igset txal 30                                              | Text alignment Hor-Right / Vert-Base
itx [xtext] [ytext] [name]                                 | Write text (name)
igset txal 10                                              | Text alignment Hor-Left / Vert-Base
itx 0.1 0.1 [sn]                                           | Write text (slide name)
igset chhe 0.3                                             | Set character height
igset lwid 2                                               | Double line width
return

