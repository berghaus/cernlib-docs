+DECK,PAWLOGON.
*CMZ :  1.13/00 06/02/92  16.03.23  by  O.Couet
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWLOGON
Alias/Create go 'Exec ALL'
+SELF,IF=APOLLO.
Alias/Create ext ftn
+SELF,IF=SGI,IBMRT,SUN,CONVEX,DECS,AIX370,HPUX,IPSC,ALLIANT.
Alias/Create ext f
+SELF,IF=IBM.
Alias/Create ext fortran
+SELF,IF=VAX.
Alias/Create ext for
+SELF.
Mess
Mess 'Enter GO to start all the examples'
Mess 'Enter GO n to start from the example n'
Mess
RETURN
+DECK,ALL.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO ALL 1=1 NBMAC=30
   i=[1]
NEXT:
   Exec MACNAME [i]
   i=[i]+1
   If [i] <= [NBMAC] Goto NEXT
RETURN

+DECK,MACNAME.
*CMZ :  1.13/00 06/01/92  09.38.46  by  O.Couet
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO MACNAME
   ch='   *'
   If $len([1])<>1 goto 10
   ch='    *'
10:
   Mess
   Mess '                     ******************************'
   Mess '                     *                            *'
   Mess '                     *   Starting MACRO PAWEX'//[1]//[ch]
   Mess '                     *                            *'
   Mess '                     ******************************'
   wait 'Enter <CR> to execute or Q to quit'
   Exec PAWEX[1]
   Selnt 1
   Text 0.02 0.02 'PAWEX'//[1] .25 0.
   call igterm
RETURN

+DECK,ALLDEF.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO ALLDEF
****************************************************************
*                                                              *
* This MACRO initialises NICE values for producing PAW manual  *
*                                                              *
****************************************************************
opt *
set *
igset *
hi/de 0
ve/de *
viewing/zone 1 1
clr
title_global ' '
title_global ' ' U
opt NBOX
set hwid 1
set fwid 1
set bwid 1
set pwid 1
igset lwid 1
set csiz 0.25
set vsiz 0.25
set tsiz 0.32
set xmgl 1.2
set xmgr 1.2
set ymgu 0.5
set ymgl 1.5
set gsiz 0.1
set yhti 0.7
set ksiz 0.15
igset mtyp 1
RETURN

+DECK,PAWEX1.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX1
****************************************************************
*                       PAW TUTORIAL EXAMPLE 1                 *
* Lines starting with a * are comments and are ignored by PAW  *
****************************************************************
Exec ALLDEF
vector/create VECT1(10)
vector/input VECT1 10 8 6 4 2 3 5 7 9 11
VECTOR/CREATE VX(20) R 1. 2. 3. 4. 5. 6. _
7. 8. 9. 10. 11. 12. 13. 14. 15. 16. 17. 18. 19. 20.
VECTOR/CREATE VY(20) R 1.1 3.2 5.3 7.4 7.5 _
6.6 4.3 2.1 6.6 11.1 16.2 18.3 19.0 17.8 16.0 12.1 9.1 6.1 3.1 6.6
zon 1 2
set htyp 244
vector/draw VECT1
GRAPH 20 VX VY
smk 21
GRAPH 20 VX VY P
set DMOD 2
GRAPH 20 VX VY C
set DMOD 0
VE/DE *
RETURN

+DECK,PAWEX2.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX2
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 2                           *
* Example wshowing some more vector commands                             *
**************************************************************************
Exec ALLDEF
vector/create VECT(10,3) R _
1. 2. 3. 4. 5. 6. 7. 8. 9. 10. _
9.1 8.1 7.1 6.1 5.1 4.1 3.1 2.1 1.1 0.1 _
6.2 4.2 3.2 2.2 1.2 1.2 2.2 3.2 4.2 5.2
VECTOR/CREATE VECT1(10) R _
1.1 2.2 3.3 4.4 5.5 6.6 5.5 4.4 3.3 2.2
set htyp 244
ve/dr VECT(1:10,3)
set HTYP 0
set HWID 2
vector/draw VECT(1:10,3) ! SC
set dmod 13
igset MSCF 3.
igset MTYP 29
set HWID 2
vector/draw VECT1 ! LPS
ve/list
ve/write VECT 'vector.data' '(3(10f5.0,/))'
ve/delete *
RETURN

+DECK,PAWEX3.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX3
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 3                           *
* Example showing various possible data representation with VECTOR/DRAW  *
**************************************************************************
Exec ALLDEF
zone 2 3
ve/create v(10) R 5 1 3 2 4 1 3 1 8 6
set htyp 244
ve/draw v
ve/draw v ! b
ve/draw v ! l
ve/draw v ! l*
ve/draw v ! bl*
igset mtyp 21
ve/draw v ! e
ve/de V
zone
RETURN

+DECK,PAWEX4.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX4
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 4                           *
* Example which shows the difference between VECTOR/DRAW and VECTOR/PLOT *
* It also shows the functioanlity of VECTOR/HFILL and PUT/CONTENTS       *
**************************************************************************
Exec ALLDEF
zone 2 2
ve/create VECT1(10) R 1 2 3 4 5 5 4 3 2 1
set htyp 244
ve/draw VECT1
SET HCOL 1001
ve/plot VECT1
set htyp 244
create/1dhisto 100 'test vector/hfill' 5 1. 6.
MAX 100 2.5
ve/hfill VECT1 100
histo/plot 100 'b'
hi/de 100
create/1dhisto 100 'test put/contents' 10 1. 11.
MAX 100 5.5
MIN 100 0.5
put/contents 100 VECT1
histo/plot 100
ve/de VECT1
hi/de 100
zone
RETURN

+DECK,PAWEX5.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX5
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 5                           *
* Example showing vector operations. The resulting vectors are created   *
* automatically.                                                         *
**************************************************************************
Exec ALLDEF
zone 1 2
ve/create V1(10) R 1 2 3 4 5 5 4 3 2 1
vector/operations/vscale V1 0.5   V12
ve/op/vscale             V1 0.25  V14
set htyp 0
ve/dr V1
ve/dr V12 ! S
ve/dr V14 ! S
vsub                      V1 V14 V14M
ve/dr V1
set htyp 344
ve/dr V14M ! S
set htyp 144
ve/dr V12  ! S
ve/de *
zone
RETURN

+DECK,PAWEX6.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX6
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 6                           *
* Example showing a simple macro, with a loop and a VECTOR fit.          *
**************************************************************************
Exec ALLDEF
ve/create VECT(10,3)
ve/read VECT 'vector.data'

ve/print VECT(1:10,3)
vbias vect(1:10,1) 0.5 vect(1:10,1)
zon 1 2
ip = 2
set htyp 244
LOOP:
  ve/draw VECT(1:10,[ip])
  order = [ip] - 1
  ve/fit VECT(1:10,1) VECT(1:10,[IP]) ! P[order] WS
  ip = [ip] + 1
  if [ip] < 4        GOTO LOOP
ve/delete VECT
RETURN

+DECK,PAWEX7.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX7
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 7                           *
* Plot a few one-dimensional functions                                   *
**************************************************************************
Exec ALLDEF
opt utit
opt grid
func/plot x*sin(x)*exp(-0.1*x)  -10. 10.
set dmod 2
func/plot (sin(x)+cos(x))**5    -10. 10. s
set dmod 3
func/plot (sin(x)/(x)-x*cos(x)) -10. 10. s
set dmod 1
opt htit
opt ngri
RETURN

+DECK,PAWEX8.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX8  1=8
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 8                           *
* Plot a one-dimensional function and loop                               *
* The Macro parameter is the number of plot to draw on the picture       *
* The defaults is 8                                                      *
**************************************************************************
Exec ALLDEF
SET DMOD 1
SET XTIC 0.0001
SET YTIC 0.0001
SET Xval 100.
SET Yval 100.
OPT utit
FUN/PLOT X*SIN(X) -10 10
FUN/PLOT X*COS(X)*SIN(X) -10 10 S
A=[1]-1
LOOP:
  FUN/PLOT X*SIN(X)*[A]/[1] -10 10 S
  FUN/PLOT X*COS(X)*SIN(X)*[A]/[1] -10 10 S
A=[A]-1
IF [A]>0 GOTO LOOP
opt htit
RETURN

+DECK,PAWEX9.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX9
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 9                           *
* Plot two-dimensional functions                                         *
**************************************************************************
Exec ALLDEF
zone 2 2
FUN2 10 abs(sin(x)/x)*(cos(y)*y) 40 -6 6 _
40 -6 6
contour 10 40 0
hi/de 10
FUN2 10 x*sin(x)*y*sin(y) 40 -10. 10. _
40 -10. 10. C
surface 10
hi/de 10
RETURN

+DECK,PAWEX10.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX10 1=100
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 10                          *
* Calculate and plot (BOX option) Mandel distribution                    *
**************************************************************************
Exec ALLDEF
fun2 10 mandel.ext [1] -2.4 .8 [1] -1.2 1.2 ' '
hi/pl 10 BOX
RETURN

+DECK,PAWEX11.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX11
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 11                          *
* Creation of one and two dimensional histograms                         *
**************************************************************************
Exec ALLDEF
OPT GRID
zon 1 2
function/fun1 100 htfun1.ext 100. 0. 1.
1dh  110 'Test 1-dim Histo' 100 0. 1. 1000.
call urout.ext(5000)
fun/fun2 200 htfun2 25. 0. 1. 25. 0. 1. C
hi/li
histogram/file 1 pawhists.rzdat 1024 N
hrout 0
close 1
hi/de 0
RETURN

+DECK,PAWEX12.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX12
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 12                          *
* Read histograms from file and plot                                     *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat
hrin  *
ldir
opt grid
hi/list
zon 2 2
hi/pl 100
set htyp 244
hi/pl 110
zone 1 2 2 'S'
hi/plot 200
h/del 0
close 1
zone
RETURN

+DECK,PAWEX13.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX13
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 13                          *
* Perform operations on histograms read from file and save result        *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat ! U
hrin  0
zon 2 2
opt grid
igset mtyp 26
hi/pl 110 e
set KSIZ 0.1
hi/pl 110 pl
zon 1 2 2 s
hi/op/add 110 110 120 0.5 0.
hi/op/add 110 110 130 0.25 0.
set htyp 245
hi/pl 110
set htyp 254
hi/pl //PAWC/120 s
set htyp 253
hi/pl //PAWC/130 s
text 0.55 95. 'LEP Very Preliminary' 0.35 25.
set htyp 0
hrout 0
close 1
hi/del 0
zone
RETURN

+DECK,PAWEX14.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX14
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 14                          *
* Different representations of 2-dim histograms                          *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat
hrin  0
hi/li
close 1
opt UTIT
zon 2 2
set htyp 0
hi/pl 200 BOX
hi/2d/con 200 20 0
igset lwid 1
hi/2d/lego 200
hi/2d/sur 200
hi/del 0
zon
RETURN

+DECK,PAWEX15.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX15
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 15                          *
* Use of subranges in histogram specifiers                               *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat
hrin  0
close 1
opt GRID
opt UTIT
zon 2 2
hi/pl 110(56:95) e
opt NGRI
set htyp 244
hi/pl 200(8:8,)      BOX
hi/pl 200(3:15,3:15) CONT
hi/pl 200(4:12,4:12) LEGO
hi/del 0
zon
RETURN

+DECK,PAWEX16.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX16
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 16                          *
* Creation of an Ntuple and first look at its contents                   *
**************************************************************************
Exec ALLDEF
Ntuple/create 10 'CERN Population' 11 ' ' 3500  _
Category Division Flag Age Service Children Grade Step Nation Hrweek Cost
Ntuple/read 10 aptuple.dat
Histo/file 1 aptuple.rzdat 1024 N
hrout 10
ntuple/print 10
zone 1 2
opt stat
set stat 110
set htyp 244
Ntuple/plot 10.Age
Ntuple/plot 10.Cost
Close 1
zone
hi/de 10
RETURN

+DECK,PAWEX17.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX17
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 17                          *
* Read an Ntuple from a histogram file                                   *
* Difference between automatic and user binning                          *
**************************************************************************
Exec ALLDEF
hi/file 2 'aptuple.rzdat'
zon 2 2
opt NSTA
opt GRID
set XTIC 0.15
set YTIC 0.15
set htyp 244
Ntuple/pl 10.age
hi/create/1dhisto 11 'Age - User binning' 45 20. 65.
set ndvx -509
Ntuple/project 11 10.Age
hi/plot 11
hi/create/1dhisto 12 'Cost - User binning' 50 0. 20000.
set ndvx
Ntuple/plot 10.cost
set ndvx -504
Ntuple/pl 10.Cost ! -12
close 2
zone
hi/de 0
RETURN

+DECK,PAWEX18.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX18
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 18                          *
* Ntuple SCAN and the use of simple selection criteria                   *
**************************************************************************
Exec ALLDEF
hi/file 2 'aptuple.rzdat'
opt grid
opt stat
set stat 110
ALIAS/CREATE DIVEP 5
ALIAS/CREATE NATFR 7
cd //PAWC
NT/SCAN //LUN2/10 nation=NATFR.and.division=DIVEP  _
! ! 5 age service children grade step
hi/create/1d 200 'Number of years at CERN' 35 0. 35.
max 200 250
set ndvx 507
set htyp 233
Nt/pl //LUN2/10.Service ! -200
ATITLE 'Years at CERN' 'Number of staff'
set htyp 344
Nt/pl //LUN2/10.Service nation=NATFR -200 ! ! S
set htyp 144
Nt/pl //LUN2/10.Service _
division=DIVEP.and.nation=NATFR -200 ! ! S
close 2
set htyp 0
hi/de 0
zone
RETURN

+DECK,PAWEX19.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX19
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 19                          *
*  Use of Ntuple masks and loop construct                                *
**************************************************************************
Exec ALLDEF
hi/file 2 'aptuple.rzdat'
hrin 0
close 2
set xtic 0.0001
1dhisto 20 'Distribution by grade' 12 3 15
opt stat
SET STAT 101
opt grid
set grid 1003
opt bar
igset barw 0.8
igset baro 0.1
max 20 700
set NDVX -12.05
set NDVY -507
set htyp 244
Ntuple/plot 10.grade ! -20
box 9.5 10.5 610 640
igset TXAL 13
igset TXFP 2
igset CHHE 0.35
itx 10.7 625 'All Staff'
set htyp 266
*  Define via a mask those people which are at the end of their grade
ntuple/mask stmask N 3500
ntuple/loop 10.grade step=15>>stmask(1)
ntuple/loop 10.grade grade>4.and.step=13>>stmask(2)
ntuple/loop 10.grade (grade=13.and.step=10).or.(grade=14.and.step=7)>>stmask(3)
Ntuple/plot 10.grade stmask(1).or.stmask(2).or.stmask(3)>>stmask(4) -20 ! ! S
ntuple/mask stmask C
box 9.5 10.5 560 590
itx 10.7 575 'Staff at end of grade'
ATITLE 'Grade' 'Number of Staff'
set htyp 0
hi/de 0
RETURN

+DECK,PAWEX20.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX20
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 20                          *
* The use of Ntuple Cuts                                                 *
**************************************************************************
Exec ALLDEF
hi/file 2 'aptuple.rzdat'
Ntuple/cut 1 MOD(FLAG,2).EQ.0
Ntuple/cut 2 MOD(FLAG,4)>1
1d 20 'Male/female and resident/non-resident Staff' 13 1 14
opt grid
set grid 1003
opt stat
SET STAT 10
set xtic 0.0001
opt bar
igset barw 0.4
igset baro 0.1
max 20 600
LABELS 1 13 AG DD DG EF EP FI LEP PE PS SPS ST TH TIS
set NDVX 13.15
set NDVY -506
Ntuple/plot 10.division ! -20
set htyp 244
Ntuple/plot 10.division 2 -20 ! ! s
igset baro 0.5
set htyp 145
Ntuple/plot 10.division 1 -20 ! ! s
set htyp 154
Ntuple/plot 10.division 1.and.2 -20 ! ! s
ATITLE 'Division' 'Number of staff'
close 2
set htyp 0
hi/de 0
RETURN

+DECK,PAWEX21.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX21
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 21                          *
* Two dimensional Ntuple distributions                                   *
**************************************************************************
Exec ALLDEF
hi/file 2 'aptuple.rzdat'
clr
2d 20 ' ' 12 3 15 16 0 16 0.
Nt/project 20 //LUN2/10.step%grade
lego 20(1:,7:) 20 40
close 2
hi/de 0
RETURN

+DECK,PAWEX22.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX22
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 22                          *
* Examples of the SIGMA processor 1                                      *
**************************************************************************
Exec ALLDEF
zone 2 2
opt GRID
set ndvx 520
sigma x=array(200,0#2*PI)
sigma sinus=sin(x)
sigma sinx=sin(x)/x
gra 200 x sinus
set dmod 2
gra 200 x sinx l
set dmod 0
SIGMA x=array(300,0#8)
SIGMA g=cosh(x)+sin(1/(.1+X*X))
gra 300 x g
SIGMA  x=array(300,0#3)
SIGMA g=cosh(x)+sin(1/(.1+X*X))
gra 300 x g
SIGMA  x=array(300,0#1)
SIGMA g=cosh(x)+sin(1/(.1+X*X))
gra 300 x g
RETURN

+DECK,PAWEX23.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX23
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 23                          *
* Examples of the SIGMA processor 2                                      *
**************************************************************************
Exec ALLDEF
zone 2 2
sigma x=array(200,0#5)
sigma A=8
sigma B=.01
sigma Y=EXP(-X)*SIN(A*X)+B*X*X
opt GRID
set ndvx 520
gra 200 x y
opt NGRI
SIGMA x=array(200,0#2*pi)
SIGMA s=sin(x)
SIGMA S2=S/2
SIGMA c=cos(x)
SIGMA c2=c/2
SIGMA s4=s/4
SIGMA c4=c/4
gra 200 s c
gra 200 s2 c l
gra 200 s4 c l
gra 200 s c2 l
gra 200 s2 c2 l
gra 200 s4 c2 l
gra 200 s c4 l
gra 200 s2 c4 l
gra 200 s4 c4 l
sigma a=array(100,0#59.77)
SIGMA NC=NCO(A)
SIGMA y=cos(a)*a
SIGMA x=sin(a)*a
GRA NC X Y
SIGMA a=a*2.55555
SIGMA y=cos(a)*a
SIGMA x=sin(a)*a
GRA NC X Y
RETURN

+DECK,PAWEX24.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX24
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 24                          *
* Operations on histograms (Keep and Update)                             *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat
Zone 1 2
opt grid
set grid 1003
set htyp 245
h/pl 120 k
set grid
set htyp 254
h/pl 110
set htyp
h/pl 110 +
set htyp 144
hi/pl 130 +
zone
close 1
RETURN

+DECK,PAWEX25.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX25
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 25                          *
* Merge pictures onto one plot                                           *
**************************************************************************
Exec ALLDEF
histogram/file 1 pawhists.rzdat
EXEC PICTMERG hid=110 s1=31 s2=40
close 1
RETURN

+DECK,PICTMERG.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PICTMERG HID=0 S1=1 S2=100
**************************************************************************
*                   PAW TUTORIAL EXAMPLE 25 - MERGE Macro                *
**************************************************************************
Exec ALLDEF
Next
Set *
Opt *
opt NBOX
Swi Z
Pic/cr MERGE2
set HTYP 354
Hi/PL [hid]
set htyp 244
Hi/PL [hid]([s1]:[s2]) s
Pic/cr MERGE1
set HTYP 354
opt utit
opt grid
Hi/PL [hid]([s1]:[s2])
opt ngri
Izpict MERGE2 c
Pi/Merge MERGE1 .45 .45 .47 D
Swi G
PI/del *
Return

+DECK,PAWEX26.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX26
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 26                          *
* Pie chart and Bar chart                                                *
**************************************************************************
EXEC ALLDEF
EXEC PIE
RETURN

+DECK,PIE.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PIE
**************************************************************************
*             Companion macro to PAW TUTORIAL EXAMPLE 26                 *
**************************************************************************
alias/cre colbackg  0
alias/cre colcompl  1
alias/cre colred    2
alias/cre colgreen  3
alias/cre colblue   4
alias/cre colyellow 5
alias/cre colpurple 6
alias/cre colcyan   7
igset bord 1
zon 2 2
null 0 20 0 20 a

v/cre vws(5) R 28.3 18.6 16.9 13.5 22.7
label 1 5 'Sun' 'DEC' 'HP' 'Apollo' 'Other'

v/cre offset(5)  R 2*0. 2*20. 0.
v/cre colour(5)  R colred colgreen colblue colyellow colpurple
v/cre style(5)   R 111 222 333 444 265

igset fais 1
pie 10. 10. 7.  5 vws p offset ! colour
null 0 20 0 20 a
igset fais 1
pie 10. 10. 7.  5 vws l offset ! colour
null 0 20 0 20 a
pie 10. 10. 7.  5 vws n offset style
null 0 20 0 20 a
pie 10. 10. 7.  5 vws l offset style
*al/de *
RETURN

+DECK,PAWEX27.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX27
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 27                          *
* Make a complex graph with PAW using Postscript fonts                   *
**************************************************************************
Exec ALLDEF
out 55 junk
EXEC COMPTIME
out -55
RETURN

+DECK,COMPTIME.
*CMZ :  1.13/00 10/02/92  09.51.09  by  O.Couet
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO COMPTIME
**************************************************************************
*             Companion macro to PAW TUTORIAL EXAMPLE 27                 *
**************************************************************************
OPT *
SET *
igset *
ve/de *
OPT NBOX
OPT LOGY
OPT TIC
OPT UTIT
opt ZFL1
size 16 20
set VSIZ 0.20
set YGTI 1.2
set XVAL 0.4
set YVAL 0.2
set XLAB 1.0
set YLAB 1.2
set XTIC 0.15
set YTIC 0.15
set ASIZ 0.26
set GSIZ 0.35
title_gl 'CERN Central Computer Usage'
vector/create vy(30) R 9.2 11.8 34.9 60.7 87.1 217.8 360 1250 2500 4006 _
  4478 5590 5910 6246 10879 12849 18429 19481 21171 25005 _
  31219 33928 37057 45520 57000 75957 98806 118993 131800 151138
sigma vx=array(30,60#89)
ve/cre f1(2) r 2*0.0
ve/cre f2(2) r 2*0.0
SET NDVX -30.05
NULL 60 90 5 250000
igset MSCF 0.75
igset mtyp 21
graph 30 vx vy p
sigma we=sqrt(vy)
ve/fi vx(:10) vy(:10) we e es ! f1
ve/fi vx(10:) vy(10:) we e es ! f2
arrow  64. 62.    10.    10.  0.15
igset txal 20
igset chhe 0.18
itx 63.    12. 'IBM 709'
arrow  65. 63.    35.    35. -0.11
itx 64.    40  'IBM 7090'
arrow 75. 65.    230.   230. -0.11
itx 70.   260. 'CDC 6600'
arrow 85. 72.   4000.  4000. -0.11
itx 78.5  4500. 'CDC 7600'
arrow 82. 78.   6500.  6500. -0.11
itx 80.   7500. 'IBM 168 '
arrow 81. 79.  10000. 10000. -0.11
itx 80.  12000. 'IBM 3032'
arrow 85. 81.  18000. 18000. -0.11
itx 83.  20000. 'IBM 3081'
igset txal 10
arrow 84. 82.  27000. 27000. -0.11
itx 82.  30000. 'SIEMENS 7880'
igset txal 20
arrow 90. 84.  42000. 42000.  0.11
itx 87.  50000. 'SIEMENS 7890'
arrow 90. 85.  68000. 68000.  0.11
itx 87.5 72000. 'IBM 3090'
arrow 90. 88.  100000. 100000.  0.11
itx 89. 110000. 'CRAY'

arise=$sigma(int((exp(f1(2))-1)*100+0.5))//'% per Annum rise'
xhand=68.
yhand=$sigma(exp(f1(1)+f1(2)*[xhand]))
EXEC DRAW X=[xhand] Y=[yhand] TEXT=[arise]
arise=$sigma(int((exp(f2(2))-1)*100+0.5))//'% per Annum rise'
xhand=84.
yhand=$sigma(exp(f2(1)+f2(2)*[xhand]))
EXEC DRAW X=[xhand] Y=[yhand] TEXT=[arise]
atitle 'Year ' 'IBM 168 Units used '
RETURN



+DECK,DRAW.
*CMZ :  1.13/00 11/09/91  16.04.48  by  Rene Brun
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO DRAW
igset TXAL 30
igset TANG -35.
igset TXFP -140
igset CHHE  0.50
itx $SIGMA([X]-0.9) [Y] +
igset TXAL 30
igset TANG  0.
igset TXFP -30
igset CHHE  0.22
y = [y] * 1.70
itx [X] [Y] [TEXT]
RETURN

+DECK,PAWEX28.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX28
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 28                          *
**************************************************************************
Exec ALLDEF
Mess 'Dummy Macro'
RETURN

+DECK,PAWEX29.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX29
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 29                          *
**************************************************************************
Exec ALLDEF
Mess 'Dummy Macro'
RETURN

+DECK,PAWEX30.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO PAWEX30
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30                          *
* Making slides with PAW and HIGZ                                        *
**************************************************************************
Exec ALLDEF
alias/create mainfont -60
opt zfl1
exec discomp
RETURN

+DECK,DISCOMP.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO DISCOMP
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30 - DISCOMP                *
* Text for slide example DISCOMP                                         *
**************************************************************************
exec slide sn='DisComp' title='Distributed Computing'
igset txfp mainfont
igset chhe 0.5
itx 2 17 'With a distributed operating system (not yet !)'
itx 2 15 'With tools on top (RPCs, NCS,.. ?)'
igset chhe 0.4
itx 3 14 Tmess
itx 3 13 Tfork
itx 3 12 Tdata
itx 3 11 Tcomp
igset txfp -70
itx 5 14 'Time to send message to remote process'
itx 5 13 'Time to fork a process'
itx 5 12 'Time to pass data (in and out)'
itx 5 11 'Time used for computation on remote process'
igset txfp mainfont
pave 2 16 2 9 0.3 0 1001 trs
igset txal 33
itx 6 7 'Efficiency ='
igset txal 20
line 6.1 7 14.1 7
itx 10 7.2 Tcomp
itx 10 6.3 'Tcomp + Tmess + Tfork + Tdata'
igset txfp -240
igset chhe 0.6
igset txal 30
itx 1.5 17 P
itx 1.5 15 P
igset chhe 0.3
igset txal 20
igset txfp 2
itx 9 4 'Many time consuming applications today have'
itx 9 3 'Efficiency > 0.9'
RETURN

+DECK,SLIDE.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
*-- Author :
MACRO SLIDE name='Author/CERN  CONF99' sn=' ' title=' '
**************************************************************************
*                       PAW TUTORIAL EXAMPLE 30 - SLIDE                  *
* General macro to draw contours of SLIDE                                *
**************************************************************************
xsize=18
ysize=22
width=0.4
xmax = [xsize]-[width]
ymax = [ysize]-[width]
size [xsize] [ysize]
next
igset *
igset lwid 2
pave 0 [xmax] 0 [ymax] [width] 0 1005 tr
igset lwid 1
xtitle = $sigma(([xsize]-0.2)/2.)
ytitle = [ysize]-1.5
igset txfp 2
igset txal 20
igset chhe 0.6
itx [xtitle] [ytitle] [title]
igset chhe 0.3
igset txal 10
xtext = [xmax]-0.2
ytext = 0.1
igset chhe 0.2
igset txal 30
itx [xtext] [ytext] [name]
igset txal 10
itx 0.1 0.1 [sn]
igset chhe 0.3
igset lwid 2
return

+PATCH,PAXFOR.
*CMZ :  1.12/00 10/07/91  19.40.31  by  Unknown
+DECK,HTFUN1.
*CMZ :  1.11/03 07/06/91  14.37.53  by  O.Couet
*-- Author :
      FUNCTION HTFUN1(X)
*
      DATA C1,C2,XM1,XM2,XS1,XS2/1.,0.5,0.3,0.7,0.07,0.12/
*
      A1=-0.5*((X-XM1)/XS1)**2
      A2=-0.5*((X-XM2)/XS2)**2
      X1=C1
      X2=C2
      IF(ABS(A1).GT.0.0001)X1=C1*EXP(A1)
      IF(ABS(A2).GT.0.0001)X2=C2*EXP(A2)
      HTFUN1=X1+X2
      END
      FUNCTION HTFUN2(X,Y)
      HTFUN2=100*HTFUN1(X)*HTFUN1(Y)
      END
+DECK,UROUT.
*CMZ :  1.09/00 04/05/90  15.50.53  by  Rene Brun
*-- Author :
      SUBROUTINE UROUT(NEV)
      DO 10 I=1,NEV
          X=HRNDM1(100,I)
          CALL HFILL(110,X,0.,1.)
   10 CONTINUE
      END

+DECK,MANDEL.
*CMZ :  1.09/00 04/05/90  15.50.53  by  Rene Brun
*-- Author :
      REAL FUNCTION MANDEL(XP)
      DIMENSION XP(2)
      DATA NMAX/30/
      X=XP(1)
      Y=XP(2)
      XX=0.
      YY=0.
            DO 30 N=1,NMAX
               TT=XX*XX-YY*YY+X
               YY=2.*XX*YY+Y
               XX=TT
               IF (4..LT.XX*XX+YY*YY) GO TO 1
   30       CONTINUE
    1       MANDEL=FLOAT(N)/FLOAT(NMAX)
      END

